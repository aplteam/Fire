 c←NewClient uri;reg
⍝ Create a Rumba client tailored for Tatin
 c←R.NewClient 0
 uri←(-'/'=¯1↑uri)↓uri
 :Trap 998
     reg←MyUserSettings.GetRegistry uri
 :Else
     reg←⍬
 :EndTrap
 :If ⍬≡reg
     :If ':'∊RemoveHttpProtocol uri
         c.Port←A.ToNum{⍵↑⍨¯1+⍵⍳'/'}{⍵↓⍨⍵⍳':'}RemoveHttpProtocol uri
     :Else
         :If 'http://'{⍺≡(≢⍺)↑⍵}uri
             c.Port←80
         :ElseIf 'https://'{⍺≡(≢⍺)↑⍵}uri
             c.Port←443
         :EndIf
     :EndIf
 :Else
     c.Secure←∨/'https://'⍷reg.uri
     :If 0=c.Port←reg.port
         c.Port←(c.Secure+1)⊃80 443
     :EndIf
 :EndIf
 c.Host←{⍵↑⍨¯1+⍵⍳':'}'/'~⍨RemoveHttpProtocol uri
⍝Done
