 tree←{level}LoadBuildList path;filename;ns
⍝ Reads the build list from file
⍝ Default for `level` is 0 (all)
⍝ If level is 1 then only entries with the level 1 are returned (level 1 is the top level)
⍝ If level is 2 then only dependencies are returned
⍝ Note that level←1 indicates a top-level package while any level>1 indicates a dependency.
 level←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'level'
 'Invalid left argument'Assert level∊0 1 2
 filename←path,'/',Reg.BuildListFilename
 :If F.IsFile filename
     ns←Reg.GetJsonFromFile filename
     tree←⍉↑ns.{⍎¨⍵}'principal' 'packageID' 'url'
     :If level=1
         tree←tree[;1]⌿tree
     :ElseIf level=2
         tree←(~tree[;1])⌿tree
     :EndIf
 :Else
     tree←0 3⍴⍬
 :EndIf
⍝Done
