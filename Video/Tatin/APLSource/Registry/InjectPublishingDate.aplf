 r←{path}InjectPublishingDate packageIDs;i;packageID;json
⍝ Returns a matrix with three columns:
⍝ [;1] packageID (without any beta part)
⍝ [;2] the publishing date as a real (YYYYMMDD.hhmmss)
⍝ [;3] the publishing date but for non-betas 99999999
⍝ Useful for sorting purposes.
 path←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'path'
 r←((≢packageIDs),3)⍴''
 :For i :In ⍳≢packageIDs
     packageID←i⊃packageIDs
     json←GetJsonFromFile(AddSlash path),packageID,'/',CFG_Name
     :If NotJustDigits{⍵↓⍨1++/2>+\'-'=⍵}{∨/'/\'∊⍵:⊃,/1↓⎕NPARTS ⍵ ⋄ ⍵}packageID
         r[i;]←packageID json.date json.date
     :Else
         r[i;]←packageID json.date 99999999
     :EndIf
 :EndFor
⍝Done
