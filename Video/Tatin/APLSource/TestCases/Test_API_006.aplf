 r←Test_API_006(stopFlag batchFlag);⎕TRAP;res1;res2;res3;res4;res5;res;tempDir;packageFolder;cfg;version;rc;more;zipfile
⍝ Exercise ⎕SE.Tatin.Pack and ⎕SE.Tatin.PublishPackage
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←GetTempDir''
 F.DeleteFile tempDir,'/',TC.UserSettings.cfg_name                           ⍝ Precaution
 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into

 packageFolder←∆GetServerRegistryFolder,'../../Source/OS'
 res←⎕SE.Tatin.Pack packageFolder tempDir
 res←⊃F.Dir tempDir,'/'
 →T.GoToTidyUp 1≠≢res
 zipfile←⊃res
 →T.GoToTidyUp'.zip'≢⎕C ¯4↑zipfile

 (F.NormalizePath tempDir,'/')⎕NCOPY F.NormalizePath⊃⊃F.Dir packageFolder,'/*.json'

 cfg←Reg.GetJsonFromFile(⊃⊃F.Dir packageFolder,'/',TC.CFG_Name)0
 version←⊃{⍺,'-',⍵}/cfg.(group name version)
 F.RmDir ∆GetServerRegistryFolder,version

 res←⎕SE.Tatin.PublishPackage zipfile'[local]'
 →T.GoToTidyUp 200≠1⊃res
 →T.GoToTidyUp~F.IsDir ∆GetServerRegistryFolder,version

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir tempDir
 #.⎕EX TC.GetTatinRootSpace'#'
 #.Tatin.Registry.Index.Compile ∆GetServerRegistryFolder
⍝Done
