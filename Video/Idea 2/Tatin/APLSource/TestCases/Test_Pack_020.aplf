 r←Test_Pack_020(stopFlag batchFlag);⎕TRAP
 ;dir;src;dist;body;cfg;zipfile;ns;qdmx
⍝ Pack a single file (function) with an incompatible "source" setting.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←GetTempDir ⍬
 (src dist)←(⊂dir,'/'),¨'src' 'dist'
 src,←'/v0.1.0'
 F.MkDir src dist

 body←'r←Hello' 'r←''World'''
 (⊂body)F.NPUT src,'/Hello.aplf'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Hello'
 cfg.version←'0.1.0'
 cfg.source←'Firlefanz.aplf'
 TC.WritePackageConfigFile src cfg

 :Trap 998
     zipfile←TC.Pack src dist
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp~∨/'"Firlefanz.aplf" is neither file nor folder'⍷qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
