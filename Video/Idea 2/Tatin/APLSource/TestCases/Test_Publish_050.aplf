 r←Test_Publish_050(stopFlag batchFlag);⎕TRAP;installFolder;qdmx;rc;msg;zipFilename
 ;dir;src;body;cfg;reg;list;index;expected;zipped;src;src3;refs
⍝ Publish a package with an invalid dependency
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←GetTempDir ⍬
 (src reg)←(⊂dir,'/'),¨'src' 'reg'
 F.MkDir src reg

 TC.Reg.CreateRegistry reg
 TC.MyUserSettings.AddRegistry'[local2]',reg

 body←':Namespace A' 'foo←{' '##.Times ⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src,'/A.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'A' '1.0.0'
 cfg.source←'A.apln'
 TC.WritePackageConfigFile src cfg
 src TC.Dependencies.Add'DoesNot-Exist-1.0.0'           ⍝ Invalid dependency

 (rc msg zipFilename)←TC.PublishPackage src reg         ⍝ You might want to install a package with a not yet published dependency
 Assert 200=rc

 installFolder←GetTempDir'/Tatin_AppPackages'
 :Trap 998
     refs←TC.InstallPackage('[local2]/Example-A-1.0.0')installFolder
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp~∨/'Package <DoesNot-Exist-1.0.0> not found in any of these Registries'⍷qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert TC.MyUserSettings.RemoveRegistry'[local2]'
 F.DeleteFile zipFilename
 Assert 0=⊃F.RmDir installFolder
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
