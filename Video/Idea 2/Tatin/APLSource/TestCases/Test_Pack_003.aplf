 r←Test_Pack_003(stopFlag batchFlag);⎕TRAP
 ;dir;src;dist;cfg;zipfile;ns;list;expected
⍝ Pack multiple files into special source folder
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←GetTempDir ⍬
 (src dist)←(⊂dir,'/'),¨'src' 'dist'
 src,←'/v0.1.0'
 F.MkDir(src,'/APLSource')dist

 ('foo1←{1}')F.NPUT src,'/APLSource/foo1.apln'
 ('foo2←{2}')F.NPUT src,'/APLSource/foo2.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_B'
 cfg.version←'0.1.0'
 cfg.source←'APLSource'
 TC.WritePackageConfigFile src cfg
 zipfile←TC.Pack src dist
 →T.GoToTidyUp 0≥≢zipfile
 list←∆GetFileListFromZipArchive zipfile
 expected←'apl-package.json' 'APLSource/foo1.apln' 'APLSource/foo2.apln'
 →T.GoToTidyUp~expected ∆LazyMatchPaths list

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
