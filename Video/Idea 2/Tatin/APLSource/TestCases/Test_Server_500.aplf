 r←Test_Server_500(stopFlag batchFlag);⎕TRAP;packageID;serverPath;qdmx;reg;oldCredentials
 ;zip;cfg;body;clientCfg;dir;src;res
⍝ Try to publish a package with an invalid API key
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←GetTempDir ⍬
 (src zip)←(⊂dir,'/'),¨'src' 'zip'
 F.MkDir src zip
 serverPath←∆GetServerPath

 body←':Namespace Test' 'foo←{' '⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src,'/Test.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_A'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 TC.WritePackageConfigFile src cfg

 packageID←'Example-Package_A-0.1.0'

 reg←TC.MyUserSettings.GetRegistry'https://localhost'
 oldCredentials←reg.api_key
 reg.api_key←'Firlefanz'
 TC.MyUserSettings.ReplaceRegistry ⎕NEW TC.DefineRegistry(,⊂reg)
 :Trap 998
     TC.PublishPackage src'https://localhost/'
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'Server: Unauthorized request'≢qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 reg.api_key←oldCredentials
 TC.MyUserSettings.ReplaceRegistry ⎕NEW TC.DefineRegistry(,⊂reg)
 Assert 0=⊃F.RmDir dir
 Assert 0=⊃F.RmDir serverPath,'/',packageID
 ∆RecompileIndexOnServer
 F.DeleteFile(739⌶0),'/',(⊃{⍺,'-',⍵}/cfg.(group name version)),'.zip'
⍝Done
