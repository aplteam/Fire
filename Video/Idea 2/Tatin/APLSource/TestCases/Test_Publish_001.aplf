 r←Test_Publish_001(stopFlag batchFlag);⎕TRAP;rc;msg;zipFilename
 ;dir;src;body;cfg;reg;list;zipfile;zip;list2
⍝ Publish a ZIP file
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←GetTempDir ⍬
 (src reg zip)←(⊂dir,'/'),¨'src' 'reg' 'zip'
 F.MkDir src reg zip

 TC.Reg.CreateRegistry reg

 TC.MyUserSettings.AddRegistry'[local2]',reg

 body←':Namespace Test' 'foo←{' '⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src,'/Test.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_A'
 cfg.version←'1.0.0'
 cfg.source←'Test.apln'
 TC.WritePackageConfigFile src cfg

 zipfile←TC.Pack src zip
 zip ⎕NCOPY src,'/',TC.CFG_Name

 (rc msg zipFilename)←TC.PublishPackage zipfile'[local2]'
 Assert 200=rc

 →T.GoToTidyUp 1≠≢list←F.ListDirs reg
 →T.GoToTidyUp(,⊂'Example-Package_A-1.0.0')≢{⊃,/1↓⎕NPARTS ⍵}¨list
 →T.GoToTidyUp 2≠≢list2←F.ListFiles(1⊃list),'/'
 →T.GoToTidyUp 0=+/'/Example-Package_A-1.0.0.zip'∘{⍺≢(-≢⍺)↑⍵}¨list2
 →T.GoToTidyUp 1≠≢(Reg.Index.GetRegistryIndex reg)+.=⎕UCS 10

 r←T._OK

∆TidyUp:
 Assert TC.MyUserSettings.RemoveRegistry'[local2]'
 Assert 0=⊃F.RmDir dir
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
