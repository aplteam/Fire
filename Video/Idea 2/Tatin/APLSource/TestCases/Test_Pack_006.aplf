 r←Test_Pack_006(stopFlag batchFlag);⎕TRAP
 ;dir;src;dist;cfg;zipfile;ns;list;expected
⍝ Pack one code file together with several "assets" files
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←GetTempDir ⍬
 (src dist)←(⊂dir,'/'),¨'src' 'dist'
 src,←'/v0.1.0'
 F.MkDir(src,'/source')dist

 ('foo1←{1}')F.NPUT src,'/source/foo1.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_B'
 cfg.version←'0.1.0'
 cfg.source←'source'
 (F.ExpandPath src,'/README.md')⎕NCOPY F.ExpandPath'README.md'
 (F.ExpandPath src,'/Managing_packages_with_Tatin.md')⎕NCOPY F.ExpandPath'docs_archive/Managing_packages_with_Tatin.md'
 cfg.assets←'README.md' 'Managing_packages_with_Tatin.md'
 TC.WritePackageConfigFile src cfg
 zipfile←TC.Pack src dist
 →T.GoToTidyUp 0≥≢zipfile
 list←∆GetFileListFromZipArchive zipfile
 expected←'apl-package.json' 'source/foo1.apln' 'README.md' 'Managing_packages_with_Tatin.md'
 →T.GoToTidyUp~expected ∆LazyMatchPaths list

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
