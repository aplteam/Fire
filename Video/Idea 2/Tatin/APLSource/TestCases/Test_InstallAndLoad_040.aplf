 r←Test_InstallAndLoad_040(stopFlag batchFlag);⎕TRAP;dir;installFolder
 ;src4;list1;list2;refs
⍝ Install and load both APL_process and CompareSimple: they have plenty of dependencies
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←GetTempDir''
 F.MkDir installFolder←dir,'/Tatin_AppPackages'

 list1←TC.InstallPackage(∆GetServerRegistryFolder,'aplteam-APLProcess')installFolder ⍝ No version number at all
 →T.GoToTidyUp 0=≢list1
 list2←TC.InstallPackage(∆GetServerRegistryFolder,'aplteam-CompareSimple-3.0.2')installFolder
 →T.GoToTidyUp 0=≢list2

 refs←TC.LoadDependencies installFolder'#.TEMP'
 →T.GoToTidyUp 0=≢refs

 →T.GoToTidyUp~'1.0.0'∆LazyMatch 2⊃#.TEMP.APLProcess.Version
 →T.GoToTidyUp~'3.0.2'∆LazyMatch 2⊃#.TEMP.CompareSimple.Version

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
