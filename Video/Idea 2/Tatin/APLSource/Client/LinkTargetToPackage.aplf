 {r}←LinkTargetToPackage(cfg source target alias);ref;parent;name
⍝ Establishes a reference in the target space pointing to the loaded package.\\
⍝ Returns the fully qualified names of what was established in the target space.
 r←⍬
 ref←⍎source
 parent←source,'.code'
 :If 0=≢alias
     name←cfg.name
 :Else
     name←alias
 :EndIf
 :If 0<≢cfg.api
     ('Not found in ',parent,': ',cfg.api)Assert 0<⍎parent,'.⎕NC ''',cfg.api,''''
     ('Is neither class nor namespace: ',cfg.api)Assert 9=⍎parent,'.⎕NC ''',cfg.api,''''
     target⍎name,'←',parent,'.',cfg.api
 :Else
     target⍎name,'←',parent
 :EndIf
 r,←target,'.',name
⍝Done
