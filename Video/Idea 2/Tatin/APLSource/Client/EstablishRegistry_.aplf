 (opCode reg)←EstablishRegistry_(uri packageID askUserFlag);stopFlag;list;options;msg;qdmx
⍝ Attempts to interrogate a Tatin Server. In case the server is down (or anything else goes awry)
⍝ then `askUserFlag` determines what to do:
⍝ * If it is 0 an error is thrown
⍝ * If it is 1 the user is asked what to do: try again, skip, or cancel
⍝ The decision is reflected by `opCode`, which is "process" or "skip"
 stopFlag←0
 opCode←'done'
 reg←⍬
 :Repeat
     :Trap 345 347
         :If 0=≢list←ListPackages_ uri,packageID
             stopFlag←1
             :Continue
         :Else
             reg←uri
             stopFlag←1
             opCode←'process'
         :EndIf
     :Else
         :If askUserFlag
             msg←(uri,' did not respond when interrogated for ',packageID)
             options←'Try again'('Skip ',uri)'Quit operation'
             :Select Select msg options
             :Case 1
                 stopFlag←0
             :Case 2
                 stopFlag←1
                 opCode←'skip'
             :Case 3
                 ('Did not respond: ',uri)''⎕SIGNAL 347
             :EndSelect
         :Else
             qdmx←⎕DMX
             qdmx.EM ⎕SIGNAL qdmx.EN
         :EndIf
     :EndTrap
 :Until stopFlag
⍝Done
