 Get←{
 ⍝ Returns all dependencies without any comment lines.
 ⍝ By default Registry aliasses are not replaced (⍺ defaults to 0).
 ⍝ If you want any Registry alias to be replaced specify 1 as left argument.
 ⍝ Note that in case there is no dependency file then '' is returned.
     ⍺←0
     replaceAliasFlag←⍺
     path←⍵
     filename←path,'/',DEP_NAME
     0=##.F.Exists filename:''
     deps←⊃##.F.NGET filename 1
     deps←deps~¨⊂⎕UCS 0 3 10        ⍝ Yes, we've seen "empty" files containing those characters!
     deps←(0<≢¨deps)/deps
     0=≢deps:deps
     bool←';'≠⊃¨deps
     deps←bool/deps                 ⍝ Get rid of comments
     deps←##.ReplaceRegistryAlias⍣replaceAliasFlag⊢deps
     deps
 }
