 mat←TakeBestVersion2 ns;principal;groupNameMajor;b
⍝ Takes the best available version; does not differantiate between principal packages and pure dependenies.
⍝ See also `TakeBestVersion1` which is considered wrong.
 mat←⍉↑ns.(principal packageID url)
 principal←(1=mat[;1])⌿mat                              ⍝ Separate principal packages from the rest
 mat←⊖mat[Reg.SortIndexForPackageIDs mat[;2];]          ⍝ Make sure latest ones are at the top
 groupNameMajor←Reg.RemoveMinorAndPatch¨mat[;2]         ⍝ Majors are consiered separate packages
 b←(⍳≢mat)=groupNameMajor⍳groupNameMajor
 mat←b⌿mat
 principal←Reg.RemoveMinorAndPatch¨principal[;2]
 mat[;1]←(Reg.RemoveMinorAndPatch¨mat[;2])∊principal
 mat←mat[⍒mat[;1];]
⍝Done
