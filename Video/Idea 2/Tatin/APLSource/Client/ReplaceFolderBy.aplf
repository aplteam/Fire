 success←old ReplaceFolderBy new;temp;flag;qdmx
⍝ Performs the following actions:
⍝ * Establish a temp name dynamically
⍝ * Rename "old" to "temp"
⍝ * Re-create the folder "old"
⍝ * Copy the contents of "new" into "old"
⍝ * Delete "new"
⍝ * Delete "temp"
 temp←'-'{~⎕NEXISTS n←⍺{(1⊃⍵),⍺,(2⊃⍵)}⎕NPARTS ⍵:n ⋄ (⍺,⍺)∇ ⍵}{⍵↓⍨-(¯1↑⍵)∊'/\'}old
 flag←0
 :Repeat
     :Trap 19 22
         temp ⎕NMOVE old
         flag←1
     :Else
         qdmx←⎕DMX
         :If ~YesOrNo'Attempt to replace ',old,' resulted in a ',qdmx.EM,' error; try again?'
             'Cancelled by user after error'⎕SIGNAL 98
         :EndIf
     :EndTrap
 :Until flag
 ⎕MKDIR old
 :Trap 0
     old ⎕NCOPY⍠('Wildcard' 1)⊣new,'/*'
     3 ⎕NDELETE new
     success←1
 :Else                      ⍝ Roll back
     3 ⎕NDELETE old
     ⎕MKDIR old
     old ⎕NMOVE⍠('Wildcard' 1)⊣temp,'/*'
     success←0
 :EndTrap
 3 ⎕NDELETE temp
