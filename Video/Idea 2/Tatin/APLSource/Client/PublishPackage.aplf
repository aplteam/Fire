 {(statusCode errMsg zipFilename)}←PublishPackage(source registry);uri;path;cfg;errMsg;statusCode;packageID
⍝ Publishes a package.\\
⍝ 1. Checks for uniqueness of the given package/version combination.
⍝ 2. Creates a dependency file for the package if required
⍝ 3. Creates a zip file for the package in a temp folder if required
⍝ 4. Moves the zip file into the Registry, either via HTTP or directly.
⍝ 5. Updates the Registry index in case it's a local Registry
⍝ Note that if ⍵ points already to a ZIP file it is **moved** into the Registry.\\
⍝ `⍵` must be a two-item vector:
⍝ 1. `source` → folder to create package from
⍝ 2. `registry` → registry to publish package to (alias or uri)
⍝ ## The explicit result
⍝ * `statusCode` is an HTTP no matter whether it is an HTTP call or not.
⍝ * `errMsg` is empty if `statusCode` is 200, otherwise it is additional informartion.
⍝ * `zipFilename` is empty in case `source` is a ZIP file, but the name of the ZIP file created along the way otherwise.
 statusCode←200 ⋄ errMsg←''
 uri←F.EnforceSlash ReplaceRegistryAlias registry
 'No URI provided'Assert 0<≢uri
 'Not just a Registry'Assert uri≡RemovePackageID uri
 ⍝ Do we need to worry about a server that hosts several Tatin servers with the same domain name but different ports?
 'Unknown Registry'Assert(⊂EnforceTrailingSlash RemovePort RemovePackageID uri)∊MyUserSettings.registries.uri
 source←RemoveFileProtocol source  ⍝ Precaution
 zipFilename←PackIfFolder source
 'Invalid URI specified: not a zip file'Assert'.zip'≡⎕C ¯4↑zipFilename
 :If Reg.IsHTTP uri
     PublishPackageToRemoteRegistry_ zipFilename registry
 :Else
     PrintToSession'  Read config file for ',source,'...'
     cfg←ReadPackageConfigFile{F.IsDir ⍵:⍵ ⋄ 1⊃⎕NPARTS ⍵,CFG_Name}source
     PrintToSession'  Attempting to publish ',source,'...'
     packageID←cfg.group,'-',cfg.name,'-',cfg.version
     (statusCode errMsg)←Reg.PublishPackage zipFilename uri
     :If 200≠statusCode
     :AndIf '.zip'≢⎕C⊃¯1↑⎕NPARTS source
         PrintToSession'  Cleaning up temp file...'
         F.DeleteFile zipFilename
         zipFilename←''
     :EndIf
 :EndIf
⍝Done
