 r←FindUserSettings name;filename;path;F;buff
⍝ Returns the folder where the user settings live.\\
⍝ First it checks whether there is a file Tatin/.tatin in the user's home folder (platform dependant). If it exists
⍝ and it is not empty then by definition it musts hold the path pointing to a user settings file. If the contents
⍝ does not point to such a file then an error is thrown.\\
⍝ If no such file exists the user settings file is expected to live in a folder Tatin/ in the the user's home folder.
⍝ If that is not the case then an error is thrown.
 F←##.FilesAndDirs
 path←GetUserHomeFolder''
 :If F.IsFile path,'/.tatin'
     buff←⊃F.NGET path,'/.tatin'
 :AndIf 0<≢buff~⎕TC
     filename←(buff~⎕TC),'/',UserSettings.cfg_name
     ('.tatin points to ',(buff~⎕TC),' but there is no file ',UserSettings.cfg_name,'?!')Assert F.IsFile filename
     :If 0=≢⊃F.NGET filename
         ('File "',filename,'" does not point to a folder')Assert F.IsDir filename
     :EndIf
 :Else
     filename←path,'/',UserSettings.cfg_name
 :EndIf
 r←F.ExpandPath filename
⍝Done
