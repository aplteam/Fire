 {buildList}←{append}SaveBuildList(buildList path);filename;ns;bool;b;groupNameMajor;topLevel;mat
⍝ Takes a dependency tree (`buildlist`) and saves it into `path`.\\
⍝ The optional left argument `append` defaults to 1, meaning that `buildlist` is
⍝ appended to an already existing build list. If set to 0 an existing build list is replaced.
 append←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'append'
 buildList[;3]←RemoveFileProtocol¨buildList[;3]
 filename←path,Reg.BuildListFilename{('/',⍺)/⍨⍺≢(-≢⍺)↑⍵}path
 :If F.IsFile filename
 :AndIf append
     ns←Reg.JSON⊃F.NGET filename
 :Else
     ns←⎕NS''
     ns.(principal packageID url)←⊂''
 :EndIf
 ns.principal,←buildList[;1]
 ns.packageID,←buildList[;2]
 ns.url,←buildList[;3]
 ns.url←{'/'@(⍸'\'=⍵)⊢⍵}¨ns.url
 mat←TakeBestVersion2 ns
 ns.(principal packageID url)←↓⍉mat
 (⊂Reg.JSON ns)F.NPUT filename 1
 buildList←mat
⍝Done
