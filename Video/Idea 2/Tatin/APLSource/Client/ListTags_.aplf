 list←{parms}ListTags_ uri;request;res;host;packageID
 :If 0=⎕NC'parms'
     parms←⎕NS''
     parms.tags←''
 :EndIf
 (host packageID)←Reg.SeparateUriAndPackageID uri
 request←R.NewRequest 0
 request.RequestTarget←'/v1/tags/',{⍵↓⍨'/'=1⍴⍵}packageID
 :If 0<≢parms.tags
     request.RequestTarget,←'?'
     :If 0<≢⊃,/parms.tags
         request.RequestTarget,←'tags=',parms.tags
     :EndIf
 :EndIf
 request.Headers,←⊂'Accept' 'application/json'
 res←host SendAndReceive request
 :If 200≠res.StatusCode
     :If 0<≢res.Content
         ('Server: ',res.Content)Assert 0
     :Else
         ('Failed with status code ',⍕res.StatusCode)Assert 0
     :EndIf
 :EndIf
 list←Reg.APLfromJSON res.Content
⍝Done
