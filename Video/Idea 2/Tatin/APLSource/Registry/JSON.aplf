 r←{format}JSON data;buff
⍝ `⍵` is a namespace with variables, for example for config parameters.\\
⍝ `←` is JSON but nicely formatted.
 r←''
 :If (,0)≢⍴data   ⍝ We need to overcome bug <01752>
     format←{0<⎕NC ⍵:⍎⍵ ⋄ 'D'}'format'
     'Invalid: "format"'Assert 1=≢format
     'Invalid: "format"'Assert format∊'DM'
     :If ⍬≢⍴data
     :AndIf (2≠⍴⍴data)∧2≠≡data
         buff←{'\\'⎕R'\\\\'⍠('Greedy' 0)⊣⍵}⍣(⍬≢⍴data)⊣data      ⍝ Backslash is the JSON escape character!
         buff←(⎕UCS 10)(≠⊆⊢)buff
         buff←(';'≠⊃¨buff)/buff
         buff←1↓⊃,/(⎕UCS 10),¨buff
     :Else
         buff←data
     :EndIf
     r←⎕JSON⍠('Format'format)('Compact' 0)('Dialect' 'JSON5')⊣↓⍣(⊃2=⍴⍴buff)⊣buff
 :EndIf
⍝Done
