 list←ListTags(uri parms);registryPath;tagList;search;bool;fuzzy;bool_;IDs;search_
⍝ Returns either a list with all tags or, in case `parms.tags` is not empty,
⍝ all tags of all packages that carry the tags specified in `parms`.
 'No registry specified'Assert 0<≢uri
 list←0 2⍴''
 registryPath←F.ExpandPath uri
 'Neither an alias nor a path?!'Assert F.IsDir registryPath
 tagList←GetTags registryPath
 :If 0<≢tagList
     :If 2=parms.⎕NC'tags'
     :AndIf 0<≢parms.tags
         search←','(≠⊆⊢)parms.tags
         (fuzzy bool)←↓⍉↑tagList[;2]∘{FindTag_ ⍺ ⍵}¨,search
         IDs←⊃∩/bool⌿¨⊂tagList[;1]
         search_←⊃∩/bool⌿¨⊂tagList[;2]
         bool_←tagList[;1]∊IDs
         tagList←bool_⌿tagList
         tagList←(~tagList[;2]∊search_)⌿tagList
     :EndIf
     :If 0<≢tagList
         list←∪tagList[;2]
         list←list[⍋list]
     :EndIf
 :EndIf
⍝Done
