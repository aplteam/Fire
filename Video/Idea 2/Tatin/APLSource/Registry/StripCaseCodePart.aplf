 name←StripCaseCodePart name;code;length;mask;⎕IO
⍝ Strip case code from a string
⍝ This code stems from Link. It was copied to Tatin because it is the only dependency on Link right now
⍝ Kai 2021-08-26 in agreement with Morten.
 ⎕IO←0
 length←(⌽name)⍳'-'
 :If ~length∊0,≢name                            ⍝ case code must be after hyphen
     code←⎕D⍳(-length)↑name
     name←((-length+1)↓name)
 :AndIf ~code∨.>7                               ⍝ case code must be octal digits
 :AndIf ~(⌈3÷⍨≢name)≠(≢code)                    ⍝ case code must be of correct length
     mask←⌽,⍉2 2 2⊤code                         ⍝ boolean mask of upper-case
 :AndIf (⊂(≢name)↓mask)∊⍬(,0)(0 0)              ⍝ mask must be padded with 0-2 zeros
     name←((≢name)↑mask){(¯1+2×⍺)⎕C ⍵}¨name
 :EndIf
