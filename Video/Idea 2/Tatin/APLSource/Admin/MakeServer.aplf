 {r}←{bumpBuild}MakeServer batchFlag;F;targetPath;zipFilename;list;msg;success
⍝ Creates a new server.\\
⍝ Call this after `MakeClient` has been called; see there for details.
⍝ It can be called from a developer's session (⍵←0) or as part of an automated "Make" process with ⍵←1.\\
⍝ In case of a problem an error is thrown if ⍵=0 and ⎕OFF 123 if ⍵=1.\\
⍝ You may call this independently from `MakeClient`.
⍝ `bumpBuild` must be a Boolean; 1 (the default) means that the build number will be bumped.
⍝ Other values result in an error or ⎕OFF 123 respectively.
 r←⍬
 bumpBuild←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'bumpBuild'
 :If batchFlag
     ⎕SHADOW'Assert'
     Assert←{(,1)≡,⍵:r←1 ⋄ ⎕OFF 123}
 :Else
     ⎕←(⎕PW-1)⍴'-'
     msg←''
     msg,←⊂'This function will close all Cider projects, delete everything that is not needed in order to'
     msg,←⊂'run a Tatin server, re-arrange stuff, set ⎕LX and finally save the workspace together'
     msg,←⊂'with everything that is needed by the server in Dist/Server.dws'
     msg,←⊂''
     msg,←⊂'Eventually all that''s required is zipped into a file <Tatin-Server-{major}.{Minor}.{Patch}.zip'
     msg,←⊂'That file is created within the Dist/ folder.'
     ⎕←⍪msg
     ⎕←''
 :EndIf
 F←##.FilesAndDirs
 :If batchFlag
 :OrIf 1 ##.Client.YesOrNo'Would you like to continue?'
     UpdateVersionNumber bumpBuild
     {}⎕SE.Cider.CloseProject''
     #.Tatin.⎕EX¨'Client' 'Todo' 'TestCases' 'TestCasesServer' 'Simu' 'Tester2'
     ⎕EX¨'Initialize_Client' 'RunTestServer' 'CreateAllRefs_Client'
     ⎕LX←'#.Tatin.Server.Run 1'
     F.MkDir'Dist'
     targetPath←'Dist/Server'
     F.RmDir targetPath
     ⎕DL 0.1
     Assert F.MkDir targetPath
     ⎕DL 0.1
     ⎕WSID←targetPath,'/Server'
     #.⎕EX'Make_'
     ⎕SE.⎕EX ⎕SE.⎕NL⍳16                                 ⍝ Attempt to avoid "Reference in ⎕SE points to #" (⎕JSON bug)
     'Plodder' 'RumbaLean'#.Tatin.⎕CY'./Assets/Runtime/Plodder/Plodder'
     0 ⎕SAVE ⎕WSID
     F.RmDir targetPath,'/Assets/'
     ⎕DL 0.1
     F.MkDir targetPath,'/Assets/'
     ⎕DL 0.1
     Assert F.IsDir targetPath,'/Assets/'
     F.RmDir targetPath,'/Registry/'
     ⎕DL 0.1
     F.MkDir targetPath,'/Registry/'
     ⎕DL 0.1
     Assert F.IsDir targetPath,'/Registry/'
     targetPath ⎕NCOPY⍠('IfExists' 'Replace')⊣'./Assets/'
     (targetPath,'/')⎕NCOPY⍠('IfExists' 'Replace')⊣'./InstallAsWindowsService.dws'
     (targetPath,'/server.ini')⎕NCOPY⍠('IfExists' 'Replace')⊣'./server.ini.RemoveMe'
     F.RmDir'Dist/Server/Assets/RunTime/Plodder'
     F.DeleteFile¨F.Dir'Dist/Tatin-Server-*.zip'
     zipFilename←'Dist/Tatin-Server-',({{⍵/⍨∧\⍵∊'.',⎕D}⍵/⍨3>+\'.'=⍵}(2⊃##.Server.Version),''),'.zip'
     targetPath ##.DotNetZip.ZipFolder zipFilename
     :If batchFlag
         ⎕OFF
     :Else
         ⎕←'*** Done;  note that the Tatin project has been closed'
     :EndIf
 :EndIf
