 response←request GetVersionList packageID;data;html;qdmx;dateFlag
 :Trap 998
     :If IsJsonRequest request.Headers
         :If dateFlag←0<≢request.QueryParameters
         :AndIf dateFlag←(⊂'publishingdate')∊request.QueryParameters[;1]
             dateFlag←⊃A.ToNum⊃request.QueryParameters[request.QueryParameters[;1]⍳⊂'publishingdate';2]
         :EndIf
         :If 0<≢data←dateFlag Reg.ListVersions G.RegistryPath,packageID
             response←request ReturnJSON Reg.JSON data
         :Else
             response←request Respond 404 1
         :EndIf
     :Else
         :If 0<≢data←1 Reg.ListVersions G.RegistryPath,Reg.RemoveMinorAndPatch packageID
             data[;2]←FormatFloatDate¨data[;2]
             html←PrepareHtmlPageForVersionList packageID data
             response←request ReturnHTML html
         :Else
             response←request Respond 404 1
         :EndIf
     :EndIf
 :Else
     qdmx←⎕DMX
     response←request Respond 400 0
     response.Content←qdmx.EM
 :EndTrap
⍝Done
