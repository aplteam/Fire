 response←Handle_PUT request;filename;path;path2;groupName;apiKey
 request.RequestTarget←('/'=1⍴request.RequestTarget)↓request.RequestTarget
 groupName←{⍵↑⍨¯1+⍵⍳'-'}request.RequestTarget
 apiKey←'api-key'GetFromHeaders request.Headers
 :If G.Credentials AcceptCredential apiKey groupName
     :If Reg.IsValidPackageID_Complete request.RequestTarget
         response←SavePackage request
     :ElseIf request IsREST_v1 request.RequestTarget
         response←request Handle_REST_Version1 request.RequestTarget
     :Else
         response←request Respond 400  ⍝ 400 ←→ Bad Request
         response.Content←'Neither valid package ID nor proper REST request'
     :EndIf
 :Else
     response←request Respond 401      ⍝ 401 ←→ Unauthorized
     response.Content←'Unauthorized request'
 :EndIf
⍝Done
