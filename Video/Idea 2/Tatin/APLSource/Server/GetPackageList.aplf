 response←request GetPackageList packageID;data;html;noOfVersions;IDs;parms;uniqueIDs;list;list2;list3;descriptionAndProjecturl;list5;mat;group;msg
⍝ Respond to a request for some or all packages, HTML or JSON
 :If Reg.IsValidPackageName packageID
     :If 0<≢group←EstablishGroup packageID
         packageID←group,packageID
     :Else
         msg←'Package ',packageID,' not found'
         :If IsJsonRequest request.Headers
             response←msg Return404 request
         :Else
             response←msg Return404AsHTML request
         :EndIf
         :Return
     :EndIf
 :EndIf
 parms←Reg.CheckParmsForListPackages ⍬
 :If (⊂'group')∊request.QueryParameters[;1]
     parms.group←{2⊃⍵[⍵[;1]⍳⊂'group';]}request.QueryParameters
 :EndIf
 :If (⊂'tags')∊request.QueryParameters[;1]
     parms.tags←{2⊃⍵[⍵[;1]⍳⊂'tags';]}request.QueryParameters
 :EndIf
 :If (⊂'date')∊request.QueryParameters[;1]
     parms.date←{⊃(//)⎕VFI ⍵}{2⊃⍵[⍵[;1]⍳⊂'date';]}request.QueryParameters
     parms.aggregate←0
 :ElseIf (⊂'aggregate')∊request.QueryParameters[;1]
     parms.aggregate←{⊃(//)⎕VFI ⍵}{2⊃⍵[⍵[;1]⍳⊂'aggregate';]}request.QueryParameters
 :EndIf
 :If (⊂'info_url')∊request.QueryParameters[;1]
     parms.info_url←{⊃(//)⎕VFI ⍵}{2⊃⍵[⍵[;1]⍳⊂'info_url';]}request.QueryParameters
 :EndIf
 :If IsJsonRequest request.Headers
     :If 0=≢data←Reg.ListPackages(G.RegistryPath,'/',packageID)parms
         response←Return404 request
     :Else
         response←request ReturnJSON Reg.JSON data
     :EndIf
 :Else  ⍝ It's a request from a Browser
     list←Reg.GetAllPackages G.RegistryPath
     list2←list Reg.ListPackages(G.RegistryPath,packageID)parms
     list3←Reg.RemoveVersionNumber¨list
     :If 0=≢uniqueIDs←list2[;1]{⍵[(Reg.RemoveVersionNumber¨⍵)⍳⍺]}⌽,list  ⍝ ⌽ them in order to get the latest description
         mat←0 3⍴''
     :Else
         descriptionAndProjecturl←↑Reg.FetchDescriptionAndPackageProjectUrl¨G.RegistryPath∘,¨uniqueIDs
         mat←list2,descriptionAndProjecturl
     :EndIf
     html←packageID PrepareHtmlPageForPackageList mat
     response←request ReturnHTML html
 :EndIf
