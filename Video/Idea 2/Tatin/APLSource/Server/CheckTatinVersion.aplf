 msg←CheckTatinVersion request;minimum;minor;major;clientVersion;clientMajor;clientMinor
⍝ Checks whether the Tatin version used on the client matches the minimum version defined
⍝ in the INI(if any) and rejects the request as 400 (bad request) if not.
⍝ The INI entry might or might not exist but if it does it must be a number with one of:
⍝ * a major number (integer)
⍝ * major.minor number (real)
 msg←''
 :If 0<≢clientVersion←request R.GetHeaderValue'x-tatin'
     clientVersion←{⍵↑⍨¯1+⍵⍳'-'}clientVersion
     (clientMajor clientMinor)←{⊃(//)⎕VFI ⍵}¨2↑{'.'(≠⊆⊢)⍵}clientVersion
 :AndIf G.INI.Exist'Config:MinimumTatinVersion'
     minimum←⍕⊃0 G.INI.Get'Config:MinimumTatinVersion'
 :AndIf 0≠≢minimum
     (major minor)←A.ToNum¨{⍵{(¯1↓⍵↑⍺)(⍵↓⍺)}⍵⍳'.'}minimum
     :If clientMajor≠major
     :OrIf clientMinor<minor
         msg←'Request came from invalid version of Tatin. Minimum version required is ',minimum
     :EndIf
 :EndIf
