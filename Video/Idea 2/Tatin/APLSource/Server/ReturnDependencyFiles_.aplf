 json←ReturnDependencyFiles_ packageIDs;ns;packageID;deps;filename
⍝ Checks for every package ID whether it's hosted or not.
⍝ * If it is then the dependency file is returned for that package ID, or ⍬ if there is no such file.
⍝ * Packages not hosted are not mentioned at all.
 ns←⎕NS''
 ns.data←''
 packageIDs←','A.Split packageIDs
 :For packageID :In packageIDs
     :If F.IsDir G.RegistryPath,packageID
         filename←G.RegistryPath,packageID,'/',Reg.DependenciesFilename
         :If F.IsFile filename
         :AndIf 0<≢deps←⊃F.NGET filename 1
             deps←deps GetAllDependencies deps
         :Else
             deps←⊂'null'
         :EndIf
         ns.data,←⊂packageID deps
     :EndIf
 :EndFor
 json←Reg.JSON ns
 ⍝Done
