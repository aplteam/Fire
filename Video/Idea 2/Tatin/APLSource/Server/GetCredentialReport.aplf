 r←GetCredentialReport(groupNames apiKeys);ind;credentials;b1;b2;b3;p;c;l
⍝ Returns a report on credentials
 r←''
 p←{'<p>',⍵,'</p>'}
 c←{'<code>',⍵,'</code>'}
 l←{'<li>',⍵,'</li>'}
 :If 0=≢groupNames
     r,←⊂p'Anybody can publish anything.'
 :Else
     b1←(,'*')∘≡¨,¨groupNames
     b2←0=≢¨apiKeys~¨' '
     :If ∨/~b1
         :If ∨/b3←(~b1)∧b2
             r,←⊂p'These groups do <b>not</b> require an API key:'
             r,←⊂'<ul>'
             r,←l∘c¨b3/groupNames
             r,←⊂'</ul>'
         :EndIf
         :If ∨/~b2
             b3←(,'*')∘≢¨,¨(~b2)/groupNames
         :AndIf ∨/b3
             :If 0=+/b1,b2
                 r,←⊂p'All groups require a specific API key.'
             :Else
                 r,←⊂p'These groups require a specific API key:'
                 r,←⊂'<ul>'
                 r,←l∘c¨b3/(~b2)/groupNames
                 r,←⊂'</ul>'
             :EndIf
         :EndIf
     :EndIf
     :If ∨/b1
         :If ∨/b2
             :If 0=≢r
                 r,←⊂p'Anybody can publish anything.'
             :Else
                 r,←⊂p'Anybody can publish any other group.'
             :EndIf
         :Else
             :If 0=≢r
                 r,←⊂p'All groups require an API key.'
             :Else
                 r,←⊂p'All other groups require a generic API key.'
             :EndIf
         :EndIf
     :EndIf
 :EndIf
