 G←ProcessCredentials G;filename;buff
⍝ Returns a matrix with {group} - API-key combinations used for authentication.
⍝ Required for any PUT and DELETE operations.
 filename←G.RegistryPath,'/Credentials.txt'
 :If 0=F.Exists filename
     ''F.NPUT filename
 :EndIf
 :If 0=≢buff←1⊃F.NGET filename 1
 :OrIf 0=≢buff←(';'≠⊃¨buff)/buff
 :OrIf 0=≢buff←(0<≢¨buff)/buff
     G.Credentials←↓⍉0 2⍴''
 :ElseIf (,'*')≡(∊buff)~⎕TC
 :OrIf '*='≡(∊buff)~⎕TC
     G.Credentials←↓⍉1 2⍴'* '
 :Else
     buff←{'='(≠⊆⊢)⍵}¨buff
     G.Credentials←↓⍉↑buff
 :EndIf
 G.CredentialsTimestamp←Reg.Timestamp2Float⊃⊃('type' 3)F.Dir filename
⍝Done
