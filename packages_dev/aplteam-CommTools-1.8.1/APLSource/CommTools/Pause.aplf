 {flag}←{lineFlag}Pause msg;input;alias;ind
 ⍝ Prints `msg` to he session and tells the user that she must press <enter> if she wants to continue.
 ⍝ By entering "`∘∘∘`" the user may interrupt `Pause`: this activates a stop.\\
 ⍝ The optional left argument `lineFlag` defaults to 0. If it's 1 then a line is printed. The length
 ⍝ is defined by `⎕PW-1`.\\
 ⍝ You can prevent `Pause` from displaying the message and requiring the user to press <enter>,
 ⍝ refer to the documentation with ]ADoc CommTools\\
 ⍝ The function will return 1 in case it did present the message and stopped, and 0 otherwise.
 lineFlag←{0=⎕NC ⍵:0 ⋄ ⍎⍵}'lineFlag'
 msg←⊃LF{⍺,⍺⍺,⍵}/⊆msg
 msg←ReplaceCRbyLF msg
 flag←1
 :If AliasChar∊msg
     :If 0<⎕NC'NoPause'
         (alias msg)←{l←⍵⍳AliasChar ⋄ (l↑⍵)(l↓⍵)}msg
         flag←~∨/({⍵↑⍨⍵⍳AliasChar}¨⊆NoPause)≡¨⊂alias
     :EndIf
 :Else
     :If 0<⎕NC'NoPause'
         :If NoPause≡1
         :OrIf ∨/(⊆NoPause)∊⊆msg
         :OrIf ∨/(NoPause/⍨~AliasChar∊¨NoPause){⍺≡¨(≢¨⍺)↑¨(≢⍺)⍴⊂⍵}{⍵↑⍨¯1+⍵⍳LF}msg
             flag←0
         :EndIf
     :EndIf
 :EndIf
 :If flag
     :If lineFlag
         ⍞←((⎕PW-1)⍴'─'),LF
     :EndIf
     :If AliasChar∊msg
         ind←msg⍳AliasChar
     :AndIf ∧/~(ind↑msg)∊' ',LF
         msg←ind↓msg
     :EndIf
     input←⍞,0/⍞←({0=≢⍵:⍵ ⋄ ⍵,LF}msg),'In order to continue press <enter> '
     :If '∘∘∘'≡¯3↑input
         (1+⊃⎕LC)⎕STOP⊃⎕SI
         ∘∘∘ ⍝ Deliberate stop caused by user input
     :EndIf
 :EndIf
⍝Done
