 {r}←{noBuiltID}UpdateVersionName(issFilename name);script;searchFor;buff;offset;length
⍝ Reads the Inno script `issFilename` and checks whether there is a line `#define MyAppVersion`.
⍝ If that is not the case, not further action is taken.\\
⍝ If that **is** the case then the version number assigned is replaced by the latest one.
⍝ Naturally if no `Version` function can be found no action will be taken either.\\
⍝ You can tell the function to **not** add a built ID to the version by passing 1 as (optional) ⍺.
 noBuiltID←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'noBuiltID'
 :If 3=⎕NC'#.',name,'.Version'
     script←⊃F.NGET issFilename
     searchFor←'#define \bMyAppVersion\b'
     :If 0<≢buff←searchFor ⎕S 0 1⍠('Greedy' 0)('IC' 1)('Mode' 'D')⊣script
         'More than one definition of "MyAppVersion" found in Inno script'Assert 1=≢buff
         (offset length)←⊃buff
         script←(offset+length)⌽script
         script←(1++/2>+\'"'=script)↓script
         script←' "',({⍵/⍨3>+\'.'=⍵}⍣noBuiltID⊣(1+⎕IO)⊃⍎'#.',name,'.Version'),'"',script
         script←(-offset+length)⌽script
         (⊂script)F.NPUT issFilename 1
     :EndIf
 :EndIf
⍝Done
