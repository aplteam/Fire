 r←these SearchIn(nc in);this;i;b;ind;gf;h;ll;start;end;lc
⍝ Performs a bit more than an ordinary search
 in←' ',⍨##.Simplify ##.CR,¨in
 r←(⍴these)⍴0
 lc←⎕C ⎕A
 start←'⎕∆⍙_:',⎕A,lc
 end←'¯∆⍙_',⎕D,⎕A,lc
 :For i :In ⍳⍴these
     this←(i⊃these)~' '
     :If 0<+/b←this⍷in                                  ⍝ No hit at all?
         ind←⍸b
         :If ~0∊⍴ind←(~in[ind-1]∊start)/ind             ⍝ Can it be a name? (left)
         :AndIf ~0∊⍴ind←(~in[ind+⍴this]∊end)/ind        ⍝ Can it be a name? (Right)
             :If ∧/gf←'.'=in[ind-1]                     ⍝ Globally referenced?
                 r[i]+←+/gf                             ⍝ Yes, all, so we are done
             :Else
                 :If nc∊3.1 4.1                         ⍝ Is it a trad fns/opr?
                     h←1↓{⍵/⍨1=+\⍵=##.CR}in             ⍝ Header without comments
                     ((h∊'←{}();')/h)←' '               ⍝ Replace special chars by a blank
                     h←##.A.DMB h                       ⍝ Remove multiple blanks
                     ll←' '##.A.Split h                 ⍝ List of Locals
                 :AndIf 0=+/(⊂this)∊ll
                     r[i]←+/b
                 :Else
                     r[i]+←⍴ind                         ⍝ Count refs
                 :EndIf
             :EndIf
         :EndIf
     :EndIf
 :EndFor
