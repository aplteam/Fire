 (r report)←{guiFlag}ProcessList list;i;name;type;noOf;_;container;nc;dummy;buff;REPORT;msg
⍝ Loops through the list and tries to find something
⍝ For result see "ScanBody".
 r←report←''
 guiFlag←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'guiFlag'
 REPORT←''
 noOf←0⊃⍴list
 {}{'Searching...'EnableProgressbar ⍵}⍣guiFlag⊣noOf

 :For i :In ⍳noOf
     :If guiFlag
         {}{⍵:GUI.n.PB.Thumb←1⌈i ⋄ ⍬}(i∊1 2 3)∨0=100|i
     :EndIf
     (container name nc type)←list[↑i;]
     buff←''
     nc container.{0=⍺:⎕NC⊂⍵ ⋄ ⍺}←name
     :If 2.1=nc
         buff←ScanVars container name type
     :ElseIf nc∊3.1 3.2 4.1 4.2
         buff←ScanFnsAndOprs container name type
     :ElseIf 9=⌊nc
         buff←ScanScripts container name type
     :EndIf
     r,←buff
     :If {0=⎕NC ⍵:0 ⋄ ⍎⍵}'∆CANCEL'
         :Leave
     :EndIf
 :EndFor
 :If 0≠≢r
     r←{(0<⍴1⊃⍵):⍵ ⋄ ({⍵.##}0⊃⍵)(1⊃'.'A.SplitPath⍕0⊃⍵),2↓⍵}¨r
 :EndIf
 r←⊃r
 {}{DisableProgressbar ⍬}⍣guiFlag⊣⍬
⍝Done
