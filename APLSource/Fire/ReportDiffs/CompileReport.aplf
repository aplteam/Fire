 {r}←CompileReport G;A;firstFlag;k;searchFor;replaceBy;realSequence;wantedSequence;noOfItems;ind
⍝ Compiles a full report. Unchanged lines are shown once but enclosed with <div class="show">
⍝ so that we can hide them all in one go via CSS.
⍝ All changed lines are shown twice, with the changes emphasized, again via CSS.
⍝ "compressType": 1=full report ⋄ 2=compressed report
 r←⍬ ⋄ A←##.A
 (⊂##.ReportDiffs.TopOfDoc 1⊃A.SplitPath G.cssFilename)##.F.NPUT G.htmlFilename 1
 WriteReport←G.htmlFilename∘{(0∊⍴⍵): ⋄ (⊂⍵)##.F.NPUT ⍺ 2}
 {}'Compile report...'##.EnableProgressbar noOfItems←-/⌽2↑⎕FSIZE G.compareFileTieNo
 firstFlag←1
 k←0
 searchFor←##.IfLower G.SearchFor
 replaceBy←##.A.DLB ##.IfLower G.ReplaceBy
 realSequence←⊃¨{(⍕⍺),'.',⍵}/¨↓(##.GetSortedHitListData ⍬)[;0 1]
 wantedSequence←⊃¨{(⍕⍺),'.',⍵}/¨↓##.G.Hits[;0 1]
 :Repeat
     {(0=5|k):##.GUI.n.PB.Thumb←⍵}k+1
     ind←1+realSequence⍳wantedSequence[k]

     CompileReport_ k ind firstFlag(G.NameClasses[ind-1])G

     firstFlag←0
 :Until noOfItems=k←k+1
 WriteReport'</div>'
 WriteReport BottomOfDoc
⍝Done
