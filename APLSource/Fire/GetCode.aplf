 (body nc)←ref GetCode name;itemNo
⍝ Result:
⍝ [1] The code for 3,4, classes and scripted namespaces,
⍝     ,⎕fmt for variables and '' for everything else
⍝ [2] Name class
 nc←ref.⎕NC name
 :If 0=nc
     ⎕SIGNAL 918            ⍝ Then it is a shadowed Fire variable - we cannot solve this!
 :Else
     :If 2=nc
         body←ref.⍎name
         nc←2.1
     :ElseIf 3=nc
         body←ref.⎕NR name
     :ElseIf 4=nc
         body←ref.⎕NR name
     :Else
         :If 0=≢name
             body←ref.{0::⍬ ⋄ ⎕SRC ⍵}ref
         :Else
             :If ⍬≡body←ref{0::⍬ ⋄ ⎕SRC ⍺⍎⍵}name
                 body←ref.{0::⍬ ⋄ ⎕SRC ⍵}name
             :EndIf
         :EndIf
         :If 0=≢body
             :Trap 0
                 :If 0=≢name  ⍝ If ⍵ is empty then ⍵ is a class instance
                     (body nc)←(⎕SRC 0 0⊃⎕CLASS ref)9.2
                 :Else
                     body←{0::⍬ ⋄ ⎕SRC 0 0⊃⎕CLASS ⍵}ref
                     :If ⍬≢body
                         nc←9.2
                     :Else
                         body←ref.⎕SRC ref.⍎name
                         nc←ref.⎕NC⊂,name
                     :EndIf
                 :EndIf
             :Else
                 (body nc)←⊂''  ⍝ No idea
             :EndTrap
         :Else
 ⍝TODO⍝              itemNo←↑∊body∘{('^ *',⍵)⎕S 2⍠('IC' 1)('Mode' 'L')⊣⍺}¨':namespace' ':class' ':interface'
             :If 0=≢itemNo←':namespace' ':class' ':interface'{∨/⍺∊(≢¨⍺)↑¨⊂⍵:0 ⋄ ⍬}⎕C↑body       ⍝ Works well in case the first item contains it (the usual case)
                 itemNo←↑↑{⍵/⍨1=≢¨⍵}':namespace' ':class' ':interface'{⍸⍺∘≡¨(≢⍺)↑¨⍵}¨⊂⎕C body   ⍝ Investigate all lines then
             :EndIf
             nc←(':namespace' ':class' ':interface'⍳⊂⎕C{⍵↑⍨⍵⍳' '}A.DLB itemNo⊃body)⊃9.1 9.4 9.5
         :EndIf
     :EndIf
 :EndIf
