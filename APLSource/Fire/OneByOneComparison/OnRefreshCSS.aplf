 r←OnRefreshCSS msg;b;was;ref;ID;n
 ⍝ Rewrite the CSS and then refreshs the browser.
 ⍝ Used to display either the old or the new text.
 (ref msg ID)←3↑msg,⊂''
 n←##.GUI.GetRefTo_n ref
 b←IsTicked n
 ⎕THIS.wasTicked←b
 was←#.CursorObj
 #.CursorObj←1
 {}RewriteCSS b
 {}WriteStatusMsg n
 n.Browser.Size←0 0
 :If 0<≢ID
     {}n.Browser{6::shy←⍬ ⋄ 1:shy←⍺.Navigate2⊂'file:///',⍵}n.∆HtmlFilename,'#',ID
 :EndIf
 {}{0::⍬ ⋄ n.Browser.Refresh ⍵}⍬
 ⎕DL 0.1
 n.Browser.Size←100 100
 #.CursorObj←was
 {}⎕NQ n.List'GotFocus'
 r←0
⍝Done
