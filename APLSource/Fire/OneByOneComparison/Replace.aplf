 newCode←Replace G;ns;fns;hits;bool;oldCode;buff
 :If ##.G.SearchIsRegEx
     ns←⎕NS''
     ns.options←##.Replace.GetRegExOptions G.OldCode ##.G
     ns.ss←##.Replace.GetRegExSearchString ##.G G.NC                ⍝ Search string
     ns.rs←##.Replace.GetRegExReplaceString ##.G ns.ss G.NC         ⍝ Replace string
     ns.i←¯1
     ns.these←G.These
     newCode←G.OldCode
     ns.lines←ns.ss ⎕S 2⍠(ns.options,⊂'Mode' 'L')⊣G.OldCode
     fns←ns∘{
         ns←⍺
         ns.i+←1
         ~ns.i∊ns.these:⍵.Match                 ⍝ No change
         ns.ss ⎕R ns.rs⍠ns.options⊣⍵.Match
     }
     buff←ns(fns{⍺.ss ⎕R ⍺⍺⍠⍺.options⊣⍵})¨newCode[∪ns.lines]
     newCode[∪ns.lines]←buff
 :Else
     :If G.DeleteFlag
         newCode←G.OldCode[(⍳≢G.OldCode)~G.LineNumbers[G.These]]
     :Else
         hits←G.Hits[G.These]
         newCode←1↓↑,/##.NL,¨G.OldCode
         newCode[hits∘.+1↓⍳≢G.SearchFor]←⊂''
         newCode[hits]←⊂1↓↑,/##.NL,¨G.ReplaceBy
         newCode←↑,/newCode
         newCode←##.NL ##.A.Split newCode
     :EndIf
 :EndIf
⍝Done
