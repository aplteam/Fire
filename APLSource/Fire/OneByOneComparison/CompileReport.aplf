 G←CompileReport(name oldCode newCode lineNumbers ind nc n2 deleteFlag);oldCode;length;ns;html;tno
⍝ Collect all the information needed and then call CompileReport_.
 G←CollectData oldCode newCode lineNumbers ind nc
 G.DeleteFlag←deleteFlag
 ##.F.DeleteFile n2.∆HtmlFilename
 tno←n2.∆HtmlFilename ⎕NCREATE 0
 tno WriteFile TopOfDoc n2.∆CssFilename
 tno WriteFile'<h1>',name,'</h1>'
 :If G.DeleteFlag
     html←##.ReplaceSpecialChars¨oldCode[∪lineNumbers]
     html,¨←{' <span class="diff-old" id="n',⍵,'-1-old"> (<b>not</b> deleted)</span><span class="diff-new" id="n',⍵,'-1-new">(to be deleted)</span>'}¨⍕¨∪lineNumbers
     html←(⍕¨∪lineNumbers){' <div class="line"><span class="lineno">[',⍺,']</span><p>',⍵,'</p></div>'}¨html
     tno WriteFile html
 :Else
     ns←⎕NS''
     ns.ss←##.G.SearchFor
     ns.rs←##.G.ReplaceBy
     ns.lineNumbers←lineNumbers
     ns.IDs←(⍕¨lineNumbers),¨'-',¨(⍕¨↑,/{1+⍳⍵}¨+⌿{⍵∘.=∪⍵}lineNumbers)
     ns.hits←G.Hits
     ns←ns ReplaceForDisplay oldCode
     tno WriteFile ns.html
 :EndIf
 tno WriteFile BottomOfDoc
 ⎕NUNTIE tno
⍝Done
