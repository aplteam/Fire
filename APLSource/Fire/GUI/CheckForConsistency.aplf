 {r}←CheckForConsistency n;hasQuote;hasComment;msg;noOf
⍝ Checks whether the user has entered...
⍝ 1. a quote but not ticked "Text".
⍝ 2. a ⍝ (lamp) but not ticked "comment"
⍝ If that's the case then the check boxes "Text" and/or "Comment" are ticked.
 r←0
 :If n.SearchFor.Text∧.=' '
     msg←⊂'You cannot search just for spaces.'
     ##.Question.ShowMsg msg
     r←1
 :Else
     hasQuote←0<noOf←+/''''=(,¨'''' '⍝.*$')⎕R(,'&')''⊣n.SearchFor.Text
     :If 0=n.Text.State
     :AndIf hasQuote
         msg←⊂'"Search for" contains ',((1<noOf)⊃'a quote'((⍕noOf),' quotes')),'but the'
         msg,←'option "Scan text" is not ticked.' ''
         msg,←⊂'Shall Fire activate the option for you?'
         :Select ##.Question.YesNoCancel msg
         :Case 1
             n.Text.State←1         ⍝ Because the user is looking for text!
         :Case 2
             r←1
         :EndSelect
     :EndIf
     hasComment←'⍝'∊'''\N*?.'''⎕R''⊣n.SearchFor.Text
     :If 0=n.Comments.State
     :AndIf hasComment
         msg←'"Search for" contains a lamp symbol but' 'the option "Scan comments" is not ticked.'
         msg,←'' 'Shall Fire activate the option for you?'
         :Select ##.Question.YesNoCancel msg
         :Case 1
             n.Comments.State←1
         :Case 2
             r←1
         :EndSelect
     :EndIf
     :If n.APL_Name.State
         :If 0≠(⎕NS'').⎕NC n.SearchFor.Text
         :AndIf ('⎕'≠↑n.SearchFor.Text)∨0≠(⎕NS'').⎕NC 1↓n.SearchFor.Text
             msg←'"Search for" does not contain a valid APL name' 'but the option "Match APL name" is ticked.'
             msg,←'' 'Shall Fire switch "Match APL name" off for you?'
             :Select ##.Question.YesNoCancel msg
             :Case 1
                 n.APL_Name.State←0
             :Case 2
                 r←1
             :EndSelect
         :EndIf
     :EndIf
 :EndIf
⍝Done
