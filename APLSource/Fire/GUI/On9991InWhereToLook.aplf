 On9991InWhereToLook←{
⍝
     (n.ReuseSearch.State):⍬                        ⍝ No need for autocomplete on a hit list
     f←n.AutoCompleteForm.Visible                   ⍝ Flag: is autocompletion visible right now?
     isQSE←'⎕SE'≡3↑1 ⎕C n.StartSearchIn.Text        ⍝ Flag: is it pointing to ⎕SE?!
     s←isQSE{⍺:⎕SE ⋄ ((0∊⍴⍵)∨'#'=1⍴,⍵):# ⋄ ⍬}n.StartSearchIn.Text  ⍝ Determine source
     (0∊⍴,s):HideAutoComplete ⍬
     _←CheckForQSE isQSE
     l←⊃⍕¨##.List s                                 ⍝ Get list of all items in either # or ⎕SE
     l←({((⍴⍵)↑[1]l)∧.=⍵},n.StartSearchIn.Text)⌿l          ⍝ Reduce the autocomplete list
     0∊⍴l:⍬⊣n.AutoCompleteList.Items←''             ⍝ Empty?! Done!
     _←ShowAutoCompleteForm ⍬                       ⍝ Make the autocomplete form visible
     n.AutoCompleteList.Items←l
     n.AutoCompleteList.SelItems←(⍴l)↑1
     w←5+1⊃n.AutoCompleteForm.GetTextSize(4+1⊃⍴⊃n.AutoCompleteList.Items)⍴'9'  ⍝ Width
     _←2 ⎕NQ n.AutoCompleteForm'Configure',n.AutoCompleteForm.Posn,(n.AutoCompleteForm.Size[0]),w
     ⍬
 }
