 r←OnAutocompleteInWhereToLook msg;f;isQSE;s;l;w
⍝ Handle the autocomplete list and also correct the contents of "Start looking here" if required.
⍝ The second bit is nasty but in the KeyPress handler we don't have the knowledge yet.
 r←⍬
 :If ~n.ReuseSearch.State                                       ⍝ No need for autocomplete on a hit list
     f←n.AutoCompleteForm.Visible                               ⍝ Flag: is autocompletion visible right now?
     isQSE←'⎕SE'≡3↑1 ⎕C n.StartSearchIn.Text                    ⍝ Flag: is it pointing to ⎕SE?!
     s←isQSE{⍺:⎕SE ⋄ ((0∊⍴⍵)∨'#'=1⍴,⍵):# ⋄ ⍬}n.StartSearchIn.Text ⍝ Determine source
     :If 0=≢s
         {}HideAutoComplete ⍬
     :Else
         {}CheckForQSE isQSE
         l←1 ##.List s 0                                        ⍝ Get list of all named NS in either # or ⎕SE
         :If 0<≢l                                               ⍝ Empty?! Guard!
             l←⊃⍕¨l[;0]                                         ⍝ Just the refs as names
             l←({((⍴⍵)↑[1]l)∧.=⍵},n.StartSearchIn.Text)⌿l       ⍝ Reduce the autocomplete list
             l←l DropAutoCompleteLevels n.StartSearchIn.Text
         :AndIf 0<≢l
             l←{⍵[##.SortAlph⍋⍵;]}l
             {}ShowAutoCompleteForm ⍬                           ⍝ Make the autocomplete form visible
             n.AutoCompleteList.Items←l
             n.AutoCompleteList.SelItems←(↑⍴l)↑1
             w←5+1⊃n.AutoCompleteForm.GetTextSize(4+1⊃⍴⊃n.AutoCompleteList.Items)⍴'9' ⍝ Width
             {}2 ⎕NQ n.AutoCompleteForm'Configure',n.AutoCompleteForm.Posn,(n.AutoCompleteForm.Size[0]),w
         :Else
             n.AutoCompleteList.Items←''
             n.StartSearchIn.Text↓⍨←-'.'=¯1↑n.StartSearchIn.Text
         :EndIf
         n.StartSearchIn.SelText←2⍴1+⍴,n.StartSearchIn.Text     ⍝ Reset the caret
     :EndIf
 :EndIf
⍝
