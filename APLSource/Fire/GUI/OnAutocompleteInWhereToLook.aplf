 OnAutocompleteInWhereToLook←{
⍝
     n.ReuseSearch.State:⍬                          ⍝ No need for autocomplete on a hit list
     f←n.AutoCompleteForm.Visible                   ⍝ Flag: is autocompletion visible right now?
     isQSE←'⎕SE'≡3↑##.A.Uppercase n.StartSearchIn.Text     ⍝ Flag: is it pointing to ⎕SE?!
     s←isQSE{⍺:⎕SE ⋄ ((0∊⍴⍵)∨'#'=1⍴,⍵):# ⋄ ⍬}n.StartSearchIn.Text  ⍝ Determine source
     (0∊⍴,s):HideAutoComplete ⍬
     _←CheckForQSE isQSE
     l←1 ##.List s 0                                ⍝ Get list of all named NS in either # or ⎕SE
     (0∊⍴l):⍬⊣n.AutoCompleteList.Items←''           ⍝ Empty?! Guard!
     l←⊃⍕¨l[;0]                                     ⍝ Just the refs as names
     l←({((⍴⍵)↑[1]l)∧.=⍵},n.StartSearchIn.Text)⌿l          ⍝ Reduce the autocomplete list
     l←l DropAutoCompleteLevels n.StartSearchIn.Text
     0∊⍴l:⍬⊣n.AutoCompleteList.Items←''             ⍝ Empty?! Done!
     l←{⍵[##.SortAlph⍋⍵;]}l
     _←ShowAutoCompleteForm ⍬                       ⍝ Make the autocomplete form visible
     n.AutoCompleteList.Items←l
     n.AutoCompleteList.SelItems←(↑⍴l)↑1
     w←5+1⊃n.AutoCompleteForm.GetTextSize(4+1⊃⍴⊃n.AutoCompleteList.Items)⍴'9'  ⍝ Width
     _←2 ⎕NQ n.AutoCompleteForm'Configure',n.AutoCompleteForm.Posn,(n.AutoCompleteForm.Size[0]),w
     n.StartSearchIn.SelText←2⍴1+⍴,n.StartSearchIn.Text           ⍝ Reset the caret
     ⍬
 }
