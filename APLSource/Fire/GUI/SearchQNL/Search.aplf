 {r}←{printToSession}Search parms;list;containerList;bool;list2;searchFor;searchIn
 r←⍬
 printToSession←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'printToSession'
 containerList←(##.##.CompileNamespaceList parms.StartSearchIn 1)[;0]
 list←(↑,/GetNames¨containerList)~⊂,'#'
 :If parms.FullPath
     list2←list
 :Else
     list2←##.##.A.Last¨list
 :EndIf
 searchFor←parms.SearchFor
 :If parms.SearchIsRegEx
     bool←searchFor∘{≢⍺ ⎕S 0⍠('IC'(~parms.Case))⊣⍵}¨list2
 :Else
     list2←parms.Case IfLower list2
     searchFor←,parms.Case IfLower searchFor
     bool←∨/¨searchFor∘⍷¨list2
 :EndIf
 list←bool/list
 :If 0=≢list
     :If printToSession
         ⎕←'Not found in ⎕NL'
     :Else
         r←0 2⍴''
     :EndIf
 :Else
     list←list,[0.5]⍕¨⎕NC∘⊂¨list
     :If printToSession
         list←↓⎕FMT list
         list←⊃(⊂'*** Search for <',searchFor,'> yielded ',(⍕≢list),' hit',((1<≢list)/'s'),':'),list
         ⎕←list
     :Else
         r←list
     :EndIf
 :EndIf
⍝Done
