 OnCheckForUpdates msg;n;code;tag;api;name;url
 #.CursorObj←1
 name←'Fire'
 n←GetRefTo_n↑,msg
 api←⎕NEW ##.##.GitHubAPIv3(,⊂'aplteam')
 :Trap 0
     tag←1↓api.GetLatestTag name
 :Else
     #.CursorObj←0
     TellErrorWhileCheckingForBetterVersion''
     :Return
 :EndTrap
 :If tag IsBetterThan{⍵↑⍨⍵⍳'+'}1⊃##.Version
     msg←⊂'There is a better version available on GitHub: ',tag~'v'
     msg,←'' 'Would you like to download this version?'
     :If ##.Question.YesOrNo msg
         :Trap 11
             url←api.GetRelease name'EXE'
         :Else
             →∆Exit,TellErrorWhileCheckingForBetterVersion''
         :EndTrap
         ##.A.GoToWebPage⍣(0<≢url)⊣url
     :EndIf
 :Else
     TellFireIsUp2Date n
 :EndIf
 #.CursorObj←0
⍝Done
