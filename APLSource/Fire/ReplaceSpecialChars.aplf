 str←{flatten}ReplaceSpecialChars str;b;b1;b2;b3
⍝ & gets &amp;
⍝ < gets &lt;
⍝ > gets &gt;
⍝ `str` must be a simple text vector.
⍝ By default the result is a vector with any replacement strings being being injected as scalars.
⍝ As a consequence any index pointing into `str` at a hit would still be valid.\\
⍝ By providing a 1 as left argument the result will be flattened.
 flatten←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'flatten'
 b1←'&'=str
 b2←'<'=str
 b3←'>'=str
 :If 0<+/b1+b2+b3
     (b1/str)←⊂'&amp;'
     (b2/str)←⊂'&lt;'
     (b3/str)←⊂'&gt;'
 :EndIf
 :If 1∊b←∧/¨CR=str
     (b/str)←⊂'<br>'
 :EndIf
 str←∊⍣(⊃flatten)⊣str
⍝Done
