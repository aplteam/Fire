 {r}←noGui Run_ testFlag;write;data;msg;buffer;n;htmlFilename;cssFilename
⍝ Sub function because of error trapping. Only called by `Run`.
⍝ `htmlFilename` is semi-global and had to be.
 r←⍬
 htmlFilename←##.GetTempFileName'htm'
 cssFilename←##.GetTempFileName'css'
 ##.A.WriteUtf8File htmlFilename(TopOfDoc(GetCss ⍬)cssFilename)
 write←'append'∘{⍺ ##.A.WriteUtf8File htmlFilename ⍵}
 :If 0≠≢data←##.G.Hits
     {}{'Compile report...'##.EnableProgressbar 1+≢⍵}⍣(0=noGui)⊣data
     :If ##.G.Negate
         :If 0=testFlag
         :AndIf 0=noGui
             msg←'Nothing to report!' 'The last search was performed with "Negate" ticked.'
             msg,←⊂'Perform "Search" again with "Search hit list" ticked and "Negate" unticked.'
             {}##.Question.ShowMsg msg
         :EndIf
     :Else
         :If ##.G.SearchIsRegEx
             buffer←(MarkUpRegExHits MarkUpAllHits)↓data
         :Else
             buffer←(MarkUpHits MarkUpAllHits)↓data
         :EndIf
         'append'##.A.WriteUtf8File htmlFilename(buffer)
         'append'##.A.WriteUtf8File htmlFilename BottomOfDoc
         :If noGui
             r←##.A.ReadUtf8File htmlFilename
             ##.F.DeleteFile htmlFilename
         :Else
             n←testFlag Display htmlFilename data
             n.∆Filename←htmlFilename
             n.∆CSSfilename←cssFilename
             r←n
         :EndIf
     :EndIf
     {}{##.DisableProgressbar ⍵}⍣(0=noGui)⊣⍬
 :EndIf
