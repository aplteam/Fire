 r←rn MarkupHitsInVars(ref name qnc ind length);rs;ss;options;data;fullName;props
⍝ rn=Running number
 r←''
 :If 0≠≢data←ref.{6::'' ⋄ ⍎⍵}name
     :If 2=⍴⍴data
         data←1↓↑,/##.CR,¨↓data
     :EndIf
     :If ##.Replace.NonReplaceVars data
         fullName←(⍕ref),'.',name
         r←⊂'<i>Not changeable and therefore not displayed</i>'
         r←(⊂'<h2 id="h-',(⍕rn),'">',(⍕rn),'. ',fullName,'</h2>'),r,⊂'</div>'
         r←(⊂'<div class="obj">'),r,(⊂'</div>')
     :Else
         :If (1=≡data)∧1=⍴⍴data
         :AndIf ∨/(⎕UCS 10 13)∊data
             ((data=⎕UCS 10)/data)←⎕UCS 13      ⍝ Get rid of all ⎕UCS 10
             data←{⍵/⍨~(2⍴⎕UCS 13)⍷⍵}data       ⍝ Remove double CRs
             data←(⎕UCS 13)##.A.Split data      ⍝ Nest
         :EndIf
         :If (1=≡data)∧1=⍴⍴data
             r,←MarkupHitsInSimpleVars data name rn ref
         :Else
             data←1 ##.ReplaceSpecialChars¨data
             options←('IC'(~##.G.Case))('Greedy' 0)('EOL' 'CR')('UCP' 1)
             ss←rs←'' ⍝ ss:Search String; rs=Replace string
             :If ~##.G.Text
                 ss,←⊂'''\N*?'''
                 rs,←⊂,'&'
             :EndIf
             ss,←⊂⍣(~1=≡ss)⊣##.G.SearchFor
             rs,←⊂⍣(~1=≡ss)⊣'<span class="diff">&</span>'
             r←(EscapeSpecialRegExChars ss)⎕R rs⍠options⊣data
             r←(∨/¨'<span class="diff"'∘⍷¨r)/r
             r[⍳¯1+≢r]←(¯1↓r),¨⊂'<br>'
             fullName←(⍕ref),'.',name
             r←(⊂'<h2 id="h-',(⍕rn),'">',(⍕rn),'. ',fullName,'</h2>'),r,⊂'</div>'
             r←(⊂'<div class="obj" id="div-no-',(⍕rn),'">'),r,⊂'</div>'
         :EndIf
     :EndIf
 :Else
     fullName←(⍕ref),'.',name
     :If 0=ref.⎕NC name
         r←⊂('h2 id="h-',(⍕rn),'"')Tag(⍕rn),'. ',fullName
         r,←⊂'p'Tag'<i>shadowed</i>'
         r←(⊂'<div class="obj">'),r,⊂'</div>'
     :Else
         props←{'Shape: ',(⍕⍴⍵),'; Rank: ',(⍕⍴⍴⍵),'; Depth: ',(⍕≡⍵)}⍎fullName
         props,←'; Data type: '
         :Select {(∪1↑¨0⍴¨∊⍵)≡¨,¨' ' 0}⍎fullName
         :Case 1 0
             props,←'Char'
         :Case 0 1
             props,←'Numeric'
         :Else
             props,←'Mixed'
         :EndSelect
         r←⊂('h2 id="h-',(⍕rn),'"')Tag(⍕rn),'. ',fullName
         r,←⊂'p'Tag props
         r←(⊂'<div class="obj">'),r,⊂'</div>'
     :EndIf
 :EndIf
