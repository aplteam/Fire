 r←runningNumber MarkUpRegExHits(ref name qnc ind length noOfHits);bool2;bool;body;orig;plus;options;ss;rs;body2;hits;fullname;type;where
 r←''
 :If 2=⌊qnc
     r←runningNumber MarkupRegExHitsInVars(ref name qnc ind length)
 :Else
     orig←0⊃ref ##.GetCode name
     :If 0<≢orig
         :If 9.1=qnc
             orig←##.RemoveFnsAndOpr orig
         :EndIf
         fullname←(⍕ref),'.',name
         type←' (',(⍕⎕NC⊂fullname),')'
         r←⊂'<div class="obj"><h2 ID="h',(⍕runningNumber),'">',(⍕runningNumber),'. ',fullname,type,'</h2>'
         body←orig
         :If 0<+/where←∨/¨body∊¨⊂'<>&',##.CR
             (where/body)←##.ReplaceSpecialChars¨where/body
             (where/body)←∊¨##.ReplaceSpecialChars¨where/body
         :EndIf
         options←('Mode' 'L')('IC'(~##.G.Case))('Greedy'##.G.Greedy)('EOL' 'CR'),##.IsVersion16OrBetter/⊂('UCP' 1)
         ss←rs←'' ⍝ ss:Search String; rs:Replace string
         :If ~##.G.Text
             ss,←⊂'''([^'']*)'''
             rs,←⊂,'&'
         :EndIf
         :If ~##.G.Comments
             ss,←⊂'⍝\N*$'
             rs,←⊂,'&'
         :EndIf
         ss,←{⊂⍣(~1=≡ss)⊣{1↓↑,/(⎕UCS 13),¨⍵}⍣(↑1<≡⍵)⊣⍵}##.G.SearchFor
         rs,←⊂⍣(~1=≡ss)⊣'<span class="diff">&</span>'
         body2←ss ⎕R rs⍠options⊣1↓##.Simplify ##.NL,¨body
         body2←##.NL ##.A.Split body2
         hits←'<span class="diff">.*</span>'⎕S 2⍠options⊣body2
         body2←({'<span class=lineno">[',(⍕⍵),']</span> '}¨⍳≢body2),¨body2
         body2←body2[∪hits]
         body2←body2,¨⊂'<br>'
         r,←⊂(↑,/body2),'</div>'
     :EndIf
 :EndIf
