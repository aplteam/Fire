 {r}←ChangeThem batchFlag;G;_;yes;these;cursorWas;noOfChangedObjects;status
⍝ Let the user change the objects either...
⍝ * one by one with the help of CompareIt! (batchFlag=0)
⍝ * all in one go (batchFlag=1)
⍝ In case "batchFlag" is true there is one final report at the end of the process
⍝ which includes all objects involved. The user then has to make the final
⍝ decision whether all the changes should get fixed or not.\\
⍝ `r` gets 1 in case something got changed. If selection is empty or the user refuses
⍝ the confirmation then `r` is 0. In case of an error `r` becomes ¯1.
 r←0
 G←⎕NS''
 :If 0<≢##.GUI.n.HitList.Data
     G.(NameClasses Names FullNames Refs filenameOld fileOldTieNo)←⊂⍬
     :If batchFlag
         cursorWas←##.SetCursorObj 1
         G.(filenameOld fileOldTieNo)←SaveThese ⍬       ⍝ Save the code as it is right now (before the replace) for comparison
         ##.SetCursorObj cursorWas
     :EndIf
     G.filenameCompare←0 ##.GetTempFileName'.dcf'       ⍝ Native file used to write the changed source code for comparison
     :If ##.F.IsFile G.filenameCompare                  ⍝ This can happen when the Fire test cases run: timing issue
         ⎕FUNTIE((##.A.dtb↓⎕NNAMES)⍳⊂G.filenameCompare)⊃⎕FNUMS,⊂⍬
         3 ⎕NDELETE G.filenameCompare
         ⎕DL 0.1
     :EndIf
     G.compareFileTieNo←G.filenameCompare ⎕FCREATE 0    ⍝ Create and open the report file

     (status noOfChangedObjects)←ChangeThem_ batchFlag  ⍝ ←←←←←←←←←←←←←← workhorse

     :If 0=status
         r←¯1                                           ⍝ Nothing found or user has cancelled or a serious error like WS FULL has occured
         ⎕NQ n.Form'GotFocus'
         →∆Exit
     :EndIf

     :If batchFlag
         :If 0<noOfChangedObjects
             G←CompileReport G
             (yes these)←ReportAllChanges G
             :If yes
                 ⎕THIS.NoOfReplacements←Establish these G.compareFileTieNo
                 r←1
             :Else
                 ##.GUI.n.(StartBtn ReplaceBtn).Active←1
             :EndIf
         :Else
             ##.Question.ShowMsg'No changes'
         :EndIf
         ⎕DL 0.1
         G.(filenameOld filenameCompare)⎕FERASE¨G.fileOldTieNo G.compareFileTieNo
         ##.F.DeleteFile G.(htmlFilename cssFilename)
         ##.F.DeleteFile G.(filenameCompare filenameOld)
     :EndIf
     {}##.DisableProgressbar ⍬
 :Else
     {}⍬(##.Question.ShowMsg ##.HonorTestFlag)'Nothing selected?!'
 :EndIf
 :Trap 6 ⋄ ⎕NQ n.Form'GotFocus' ⋄ :EndTrap

∆Exit:
 ⎕FUNTIE((##.A.dtb↓⎕FNAMES)⍳⊂G.filenameCompare)⊃⎕FNUMS,⊂⍬
 ##.F.DeleteFile G.filenameCompare
⍝Done
