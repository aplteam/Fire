 {n}←{testFlag}Run debugFlag;TheCounterForm
⍝ Fire's main function.\\
⍝ Set `debugFlag` to 1 and you get some developer-specific options making it easier to debug Fire.\\
⍝ `testFlag`, if specified, must be a Boolean. If it is true then a number of things are different:
⍝ Replace does not execute `⎕DQ`, the comparison tool is not really called etc. Used only when the\\
⍝ test cases are executed.\\
⍝ Usually `testFlag` is specified by the test cases, not by a user.\\
⍝ Note that the `G` (for "Globals") namespace is re-used when it exists.\\
⍝ This means that a user gets the defaults when Fire is called for the very first time
⍝ but can carry on with the last settings during that session or until either Fire
⍝ is started with the `-fl` (force load) setting or `⎕SE.Fire.Reset` is called.\\
⍝ However, when `0<+/testFlag` then `Reset` is always called in order to make
⍝ sure that the test cases always start with the defaults.\\
⍝ Home page: <https://github.com/aplteam/Fire>
 testFlag←↑{0<⎕NC ⍵:(⍎⍵),0 ⋄ 0}'testFlag'
 {}Cleanup
 :If debugFlag
     ##.FilesAndDirs.PolishCurrentDir
 :EndIf
 Init ⍬
 :If 0=⎕NC'G'
 :OrIf testFlag
     G←Populate_G testFlag
 :EndIf
 G.TestFlag←testFlag
 G.HelpPath←FindFireReadMe ⍬
 n←GUI.n←GUI.CreateGUI debugFlag
 ResetCertainRegKeySettings ⍬
 :If ~testFlag
     :Trap 6 ⋄ ⎕DL 0.05 ⋄ ⎕NQ GUI.n.SearchFor'GotFocus' ⋄ :EndTrap
 :EndIf
