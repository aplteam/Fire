:Class Make_

    ∇ Run flag;targetPath;home;M;name;setupExe;myUCMDsFolder;C;tempFolder;data;buff;cfg;parms
    ⍝ Note that this is executed via the "Load" parameter in a separate and new instance of Dyalog as part of "Making" a new version
      :Access Public Shared
    ⍝ Version 8.0.1 ⋄ 2024-11-17
    ⍝ * 8.0.1 ⋄ 2024-11-17
    ⍝   * Increasing build number had no effect because the project was imported rather than opened.
    ⍝ * 8.0.0 ⋄ 2024-10-18
    ⍝   * Now a separate instance of Dyalog is started for making a new verson
      ⎕IO←1 ⋄ ⎕ML←1
      :If flag
          home←1⊃1 ⎕NPARTS''
          targetPath←home,'Dist/'
          :If 0=⎕SE.⎕NC'MakeHelpers'
          :AndIf 0=⎕SE.Tatin.LoadPackages'https://tatin.dev/aplteam/MakeHelpers' '⎕SE'
              ⎕OFF 123
          :EndIf
          ⎕FX↑'r←M' 'r←⎕SE.MakeHelpers'  ⍝ avoids creating a link in # that points to ⎕SE at the expense of defeating auto-complete
          parms←⎕SE.Cider.CreateOpenParms''
          parms.batch←1
          parms.folder←home
          buff←⎕SE.Cider.OpenProject parms
          :If ~∨/'Loading project from '⍷∊buff
              ⎕OFF 123
          :EndIf
          C←#.Fire.CiderConfig
          myUCMDsFolder←M.GetMyUCMDsFolder'\'
          name←'Fire'
          tempFolder←(M.##.F.GetTempSubDir name),'/'
         ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
          M.RecreateFolder targetPath
          M.IncreaseBuildIDInFunction'#.Fire.Fire.Version'
          :If ⎕NEXISTS M.GetMyUCMDsFolder'/Fire/Fire_uc.dyalog'
              M.CopyBetter(home,'APLSource/Fire_uc.dyalog')(M.GetMyUCMDsFolder'/Fire/Fire_uc.dyalog')'Project version' '[MyUCMDs] version'
          :EndIf
          {}⎕SE.Cider.CloseProject''  ⍝ Precaution
          cfg←#.Fire.CiderConfig
          ⎕WSID←tempFolder,name,'.dws'
          0 ⎕SAVE ⎕WSID
          #.⎕EX #.⎕NL⍳16
          ⎕LX←'#.',name,'.Run 0'
          '#.Fire.Fire'#.⎕CY ⎕WSID
          0 ⎕SAVE ⎕WSID
          './packages'M.CopyPackageFolderTo tempFolder,'packages'
          M.ConvertMarkdownToHtml5(home,'Docs/')(home,'html/')
          (home,'html/*')M.CopyTo tempFolder
          (home,cfg.CIDER.source,'/',name,'_UC.dyalog')M.CopyTo tempFolder
          (home,'LICENSE')M.CopyTo tempFolder
          (home,'Fire.ico')M.CopyTo tempFolder
          (home,'Fire.iss')M.CopyTo tempFolder
          data←⊃⎕NGET tempFolder,'Fire.iss'
          data←M.##.F.EnforceSlash tempFolder{ss←'<<TARGETDIR>>' ⋄ sp←¯1+1⍳⍨ss⍷⍵ ⋄ (-sp)⌽⍺,(≢ss)↓sp⌽⍵}data
          (⊂data)⎕NPUT(tempFolder,'Fire.iss')1
          :If 0=#.⎕NC'Inno'
          :AndIf 0=⎕SE.Tatin.LoadPackages'https://tatin.dev/aplteam/Inno'#
              ⎕OFF 123
          :EndIf
          setupExe←tempFolder #.Inno.Run name
          targetPath ⎕NMOVE setupExe
          setupExe←⊃,/1↓⎕NPARTS setupExe
          M.##.F.RmDir tempFolder
          :If 1 M.YesOrNo'Would you like to execute "',setupExe,'" ?'
              ⎕CMD targetPath,setupExe
          :EndIf
          M.##.F.RmDirByForce tempFolder
          ⎕OFF
      :EndIf
    ∇

:EndClass
