 R←Test_Replace_224(stopFlag batchFlag);n;n2;⎕TRAP;buffer;search;code
⍝ Replace a string within two dfns defined on a single line together with some vars definitions and a comment
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

⍝ Preconditions
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 buffer←⊂':Namespace Foo'
 buffer,←⊂'t1←''te{xt''⋄f0←{''⍝''}⋄f1←{abc←123⋄a{⍺×⍵}x←2}⋄abc←123⋄f2←{abc←123}⋄f3←{⍺×⍵}⋄t2←''More te}xt''⍝ comment'
 buffer,←⊂':EndNamespace'
 #.TEMP.⎕FIX buffer

 1 #._Fire.Fire.Run 0
 n←#._Fire.Fire.GUI.n

 n.SearchFor.Text←'abc←123'
 n.LookIn.Text←'#.TEMP'
 n.(Vars Comments Text).State←0

 {}∆Select n.StartBtn
 0.2 ∆Process n.Form
 →GoToTidyUp 3≠#._Fire.Fire.G.NoOfObjects
 →GoToTidyUp~n.HitList.Items Match'Foo' 'f1' 'f2'

 {}∆Select n.ReplaceBtn
 ∆Process n.Form

 n2←#._Fire.Fire.Replace.n
 n2.ReplaceBy.Text←'abcd←2000'

 {}∆Select n2.StartBtn
 ∆Process n.Form
 →PassesIf 3=#._Fire.Fire.Replace.NoOfReplacements
 code←⎕SRC #.TEMP.Foo
 →PassesIf(+/'abcd←2000'⍷1↓↑,/(⎕UCS 13),¨code)=3
 R←∆OK

∆TidyUp:
 CloseFire
