 R←Test_Replace_074(stopFlag batchFlag);n;Text;n2;allCode;⎕TRAP
⍝ Add a change marker on changed line
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

⍝ Preconditions
 1 #._Fire.Fire.Run 0
 n←#._Fire.Fire.GUI.n
 'Test_74'#.TestData.⎕NS{(⊂'.',⍨⍕⍵),¨⍵.⎕NL-3 4}#.TestData.A3
 #.TestData.A3 ∆CopyClassesTo #.TestData.Test_74

 n.SearchFor.Text←'b←'
 n.LookIn.Text←'#.TestData.Test_74'
 n.Case.State←1
 n.Comments.State←0

 {}∆Select n.StartBtn
 ∆Process n.Form
 →GoToTidyUp 0=#._Fire.Fire.G.NoOfObjects

 {}∆Select n.ReplaceBtn
 ∆Process n.Form

 n2←#._Fire.Fire.Replace.n
 n2.ReplaceBy.Text←'B←'
 n2.MarkChanges.(Active State)←1
 n2.MarkChangesText.(Active BCol)←1 0
 n2.MarkChangesText.Text←'Changed by Fire (this line)'
 n2.MarkChangesType.SelItems←0 0 1
 n2.MarkChangesType.Active←1

 {}∆Select n2.StartBtn
 ∆Process n.Form
 →GoToTidyUp 6≠#._Fire.Fire.Replace.NoOfReplacements
 allCode←1↓∊(⎕UCS 13),¨↑,/∆GetCode¨'#.TestData.Test_74.'∘{⍺,⍕⍵}¨#.TestData.Test_74.⎕NL-3 4 9.4
 →GoToTidyUp 8≠+/'Changed by Fire (this line)'⍷allCode
 R←∆OK

∆TidyUp:
 #.TestData.⎕EX'Test_74'
 CloseFire
