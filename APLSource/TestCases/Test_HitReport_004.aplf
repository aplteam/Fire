 R←Test_HitReport_004(stopFlag batchFlag);n;⎕TRAP;expected;name;ref;html
⍝ Exercise a specific RegEx and then ask for "Hit Report". This checks a simple variable.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

⍝ Preconditions
 1 #.Fire.Fire.Run 0
 n←#.Fire.Fire.GUI.n

 name←'Test_HitReport_004'
 #.⎕SHADOW name
 ref←⍎name #.⎕NS''
 ref.vars←'this is caspar david albrecht'

 n.SearchFor.Text←'\b\w*a\w*\b'   ⍝ Find all words that contain an "a"
 n.LookIn.Text←'#.',name
 n.SearchIsRegEx.State←1
 n.Greedy.(Active State)←1 0

 {}∆Select n.StartBtn
 ∆Process n.Form
 →T.GoToTidyUp 1≠↑⍴n.HitList.ReportInfo
 html←1 ##.Fire.ReportHits.Run 1
 expected←'>this is <span class="diff">caspar</span> <span class="diff">david</span> <span class="diff">albrecht</span><'
 →T.GoToTidyUp 0=+/expected⍷∊html

 R←T._OK

∆TidyUp:
 CloseFire
⍝Done
