 R←Test_Search_080(stopFlag batchFlag);n;n2;⎕TRAP;buffer;search
⍝ Ordinary search, then a search on the hit list with "Negate" ticked.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

⍝ Preconditions
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.⎕FX'r←hello' 'r←''Universe'''
 #.TEMP.⎕FX'r←Hello1' 'r←''Universe1'''
 #.TEMP.⎕FX'r←Hello2' 'r←''Universe2'' ⍝ XXXX'
 #.TEMP.⎕FX'r←Hello3' 'r←''Universe3'''
 #.TEMP.⎕FIX':Class Hello' '∇r←Hello' 'r←''Universe9''' '∇' ':EndClass'

 1 #.Fire.Fire.Run 0
 n←#.Fire.Fire.GUI.n

 n.SearchFor.Text←'Hello'
 n.StartSearchIn.Text←'#.TEMP'
 n.Case.State←0

 {}∆Select n.StartBtn
 ∆Process n.Form
 →T.GoToTidyUp 5≠#.Fire.Fire.G.NoOfObjects

 n.(ReuseSearch Negate).State←1
 n.SearchFor.Text←'XXXX'

 {}∆Select n.StartBtn
 ∆Process n.Form
 →T.GoToTidyUp 4≠#.Fire.Fire.G.NoOfObjects
 R←T._OK

∆TidyUp:
 CloseFire
