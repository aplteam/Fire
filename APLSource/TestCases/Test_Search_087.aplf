 R←Test_Search_087(stopFlag batchFlag);n;n2;⎕TRAP;buffer;search;expected
⍝ Ordinary search, then a second search on the hit list without any changes but the search string.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

⍝ Preconditions
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.⎕FX'r←Hello1' 'r←''Universe'''
 #.TEMP.⎕FX'r←Hello2' 'r←''Universe1'' ''⍝EXTRA'''
 #.TEMP.⎕FX'r←Hello3' 'r←''Universe2'''
 #.TEMP.⎕FX'r←Hello4' 'r←''Universe3'' ''⍝extra'''
 #.TEMP.⎕FIX':Class Foo1' '∇r←Hello' 'r←''Universe9''' '∇' ':EndClass'
 #.TEMP.⎕FIX':Class Foo2' '∇r←Hello' 'r←''Universe9''' '⍝Extra' '∇' ':EndClass'
 #.TEMP.⎕FIX':Namespace NS1' '∇r←myFns1' 'r←''Universe10''' '⍝Hello' '∇' ':EndNamespace'
 #.TEMP.⎕FIX':Namespace NS2' '∇r←myFns2' 'r←''Universe11''' '⍝Hello Extra' '∇' ':EndNamespace'
 #.TEMP.⎕FIX':Interface IF1' '⍝Hello' ':EndInterface'
 #.TEMP.⎕FIX':Interface IF2' '⍝Hello' '⍝Extra' ':EndInterface'
 #.TEMP.myInstance←#.TEMP.⎕NEW #.TEMP.Foo2
 'form1'#.TEMP.⎕WC'Form'('Visible' 0)('KeepOnClose' 0)  ⍝ NOT to be found (because of KeepOnClose=0)!
 #.TEMP.form1.⎕FX'FormFns1' 'Hello' 'Extra'
 'form2'#.TEMP.⎕WC'Form'('Visible' 0)('KeepOnClose' 1)
 #.TEMP.form2.⎕FX'FormFns2' 'Hello' 'Extra'

 1 #.Fire.Fire.Run 0
 n←#.Fire.Fire.GUI.n

 n.SearchFor.Text←'hello'
 n.LookIn.Text←'#.TEMP'
 n.Case.State←0

 {}∆Select n.StartBtn
 0.2 ∆Process n.Form
 →GoToTidyUp 11≠#.Fire.Fire.G.NoOfObjects
 expected←'Foo1' 'Foo2' 'FormFns2' 'Hello1' 'Hello2' 'Hello3' 'Hello4' 'IF1' 'IF2' 'myFns2' 'myFns1'
 →GoToTidyUp~expected ∆LazyMatch n.HitList.Items
 expected←'#.TEMP' '#.TEMP' '#.TEMP.form2' '#.TEMP' '#.TEMP' '#.TEMP' '#.TEMP' '#.TEMP' '#.TEMP' '#.TEMP.NS2' '#.TEMP.NS1'
 →GoToTidyUp~expected ∆LazyMatch n.HitList.ReportInfo[;0]

 n.ReuseSearch.State←1
 n.SearchFor.Text←'extra'

 {}∆Select n.StartBtn
 ∆Process n.Form
 →GoToTidyUp 6≠#.Fire.Fire.G.NoOfObjects
 R←∆OK

∆TidyUp:
 CloseFire
