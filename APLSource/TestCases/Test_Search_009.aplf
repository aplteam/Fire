 R←Test_Search_009(stopFlag batchFlag);n;⎕TRAP;expected
⍝ Search for a script but check that a reference pointing to that script is ignored
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

⍝ Preconditions
 #.⎕SHADOW'FireAcreTest'
 'FireAcreTest'#.⎕NS''
 'FireAcreTest.First'#.⎕NS''
 'FireAcreTest.Second'#.⎕NS''
 #.FireAcreTest.First.⎕FX'r←Doe dummy' 'r←''Johnny ''' 'r,←''in the world'''
 #.FireAcreTest.First.⎕FIX':Class MyClass' '∇r←Hello' ':Access Public Shared' 'r←''World11''' '∇' '∇co' ':Implements constructor' ':Access Public' '∇' ':EndClass'
 #.FireAcreTest.First.⎕FIX':Namespace MyNamespace' '∇r←Hello3' ':Access Public Shared' 'r←''World''' '∇' 'Hello4←{''world''}' ':EndNamespace'
 #.FireAcreTest.Second.Doe←#.FireAcreTest.First.Doe
 #.FireAcreTest.Second.MyClass←#.FireAcreTest.First.MyClass
 #.FireAcreTest.Second.MyNamespace←#.FireAcreTest.First.MyNamespace

 n←1 #.Fire.Fire.Run 0

 n.SearchFor.Text←'world'
 n.StartSearchIn.Text←'#.FireAcreTest'
 n.Case.State←0

 {}∆Select n.StartBtn
 0.2 ∆Process n.Form
 →T.PassesIf 5=#.Fire.Fire.G.NoOfObjects   ⍝ The one in the unnamed namepace is not found!
 expected←'Doe' 'Doe' 'Hello3' 'Hello4' 'MyClass'
 →T.GoToTidyUp expected≢n.HitList.Items

 R←T._OK

∆TidyUp:
 #.⎕EX'FireAcreTest'
 CloseFire
