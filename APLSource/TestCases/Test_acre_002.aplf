 R←Test_acre_002(stopFlag batchFlag);n;n2;oldPath;⎕TRAP;openAcreProjects;acreTestProjectName;changeFilesFolder;acreTestProjectFolder;more;en;rc
⍝ Check whether acre 4 (or later) reacts correctly when an object is deleted by Fire and there are self-refs around.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If ~IsAcre6OrBetter
     →0,R←T._Inactive
 :EndIf

 R←T._Failed

⍝ Preconditions
 #.⎕EX'FireAcreTest'
 'FireAcreTest'#.⎕NS''

 #.FireAcreTest.selfRef←#.FireAcreTest
 'FireAcreTest.Level2'#.⎕NS''
 'FireAcreTest.Level2.Level3'#.⎕NS''
 #.FireAcreTest.Level2.Level3.ref←#.FireAcreTest

 #.FireAcreTest.⎕FX'r←Doe' 'r←''Johnny ''' 'r,←''in the world'''
 #.FireAcreTest.⎕FX'r←Foe' 'r←''Anna ''' 'r,←''in the world'''
 #.FireAcreTest.⎕FIX':Class Foo' '∇r←Hello' ':Access Public Shared' 'r←''World''' '∇' ':EndClass'
 #.FireAcreTest.ref←⎕NS''
 #.FireAcreTest.ref2←#.FireAcreTest.⎕NS''
 #.FireAcreTest.ref.⎕FX'r←DeleteMe' 'r←''This is DeleteMe''' '⍝world'
 #.FireAcreTest.⎕FIX':Class MyClass' '∇Hello' 'r←''world''' '∇' '∇co' ':Access Public' 'Implements constructor' '∇' ':EndClass'
 #.FireAcreTest.⎕FIX':Namespace MyNamespace' '∇HelloNS' 'r←''world''' '∇' ':EndNamespace'
 'MyForm'#.FireAcreTest.⎕WC' Form'('Visible' 0)('KeepOnClose' 1)
 #.FireAcreTest.MyForm.⎕FX'r←Universe' 'r←''Includes world'''
 {}#.FireAcreTest.⎕NEW #.FireAcreTest.MyClass
 acreTestProjectName←'FireAcreTestTemp'
 (rc en more)←##.FilesAndDirs.RmDir'.\',acreTestProjectName
 more Assert 0=rc
 {}⎕SE.UCMD'acre.CreateProject ',acreTestProjectName,' #.FireAcreTest -casecod=''on'' -keephistory=''on'''

 1 #.Fire.Fire.Run 0
 n←#.Fire.Fire.GUI.n

 n.SearchFor.Text←'world'
 n.StartSearchIn.Text←'#.FireAcreTest'

 n.Negate.State←0
 n.ReuseSearch.State←0
 n.Case.State←0

 {}∆Select n.StartBtn
 ∆Process n.Form
 →T.GoToTidyUp 6≠#.Fire.Fire.G.NoOfObjects

 n.HitList.SelItems←n.HitList.Items∊'Doe' 'Foe'
 {}#.Fire.Fire.GUI.LetAcreDelete n.HitList.Items⍳'Doe' 'Foe'
 0.2 ∆Process n.Form

 →T.GoToTidyUp~∧/~'Doe' 'Foe'∊#.FireAcreTest.⎕NL-⍳16
 changeFilesFolder←'expand'##.FilesAndDirs.NormalizePath acreTestProjectName,'\.acre\changes\'
 →T.GoToTidyUp~'delete' 'delete'≡##.APLTreeUtils.Last¨↑##.FilesAndDirs.Dir changeFilesFolder
 R←T._OK

∆TidyUp:
 #.⎕EX'FireAcreTest'
 ##.FilesAndDirs.RmDir acreTestProjectName
 CloseFire
