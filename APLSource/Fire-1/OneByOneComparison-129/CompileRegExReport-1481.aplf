 G←CompileRegExReport(name oldCode newCode lineNumbers ind length nc n2 deleteFlag);oldCode;length;ns;fns;html;tno
⍝ Collect all the information needed and then call CompileReport_.
 G←CollectData oldCode newCode lineNumbers ind nc
 G.DeleteFlag←deleteFlag
 ##.F.DeleteFile n2.∆HtmlFilename
 tno←n2.∆HtmlFilename ⎕NCREATE 0
 tno WriteFile TopOfDoc n2.∆CssFilename
 tno WriteFile'<div id="title">',name,'  (',(⍕⎕NC⊂name),')</div>'
 ns←⎕NS''
 ns.options←##.Replace.GetRegExOptions oldCode ##.G
 ns.ss←##.Replace.GetRegExSearchString ##.G G.NC                ⍝ Search string
 ns.rs←##.Replace.GetRegExReplaceString ##.G ns.ss G.NC         ⍝ Replace string
 ns.lines←ns.ss ⎕S 2⍠(ns.options,⊂'Mode' 'L')⊣oldCode
 ns.IDs←(⍕¨lineNumbers),¨'-',¨(⍕¨↑,/{1+⍳⍵}¨+⌿{⍵∘.=∪⍵}lineNumbers)
 ns.no←⍳⍴G.Length
 ns.i←0
 fns←ns∘{ns←⍺ ⋄ id←⍕ns.i⊃ns.IDs ⋄ ns.i+←1 ⋄ '<span class="diff-old" id="n',id,'-old">',⍵.Match,'</span><span class="diff-new" id="n',id,'-new">',ns.rs,'</span>'}
 html←ns.ss ⎕R fns⍠ns.options⊣##.A.dlb oldCode[∪ns.lines]
 html←(⍕¨∪lineNumbers){'<div class="line"><span class="lineno">[',⍺,']</span><code>',⍵,'</code></div>'}¨html
 tno WriteFile html
 tno WriteFile BottomOfDoc
 ⎕NUNTIE tno
⍝Done
