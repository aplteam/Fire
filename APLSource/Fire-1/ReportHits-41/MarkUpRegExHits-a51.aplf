 r←runningNumber MarkUpRegExHits(ref name qnc ind length noOfHits);bool2;bool;body;orig;plus;options;ss;rs;body2
 r←''
 :If 2=⌊qnc
     r←runningNumber MarkupRegExHitsInVars(ref name qnc ind length)
 :Else
     orig←0⊃ref ##.GetCode name
     :If ~0∊⍴orig
         r←⊂'<h2>',(⍕runningNumber),'. ',(⍕ref),'.',name,'</h2>'
         body←∊¨ExchangeHtmlChars orig
         options←('Mode' 'M')('IC'(~##.G.Case))('Greedy'##.G.Greedy)('EOL' 'CR')('DotAll'##.G.DotAll),##.IsVersion16OrBetter/⊂('UCP' 1)
         ss←rs←'' ⍝ ss:Search String; rs=Replace string
         :If ~##.G.Text
             ss,←⊂'''([^'']*)'''
             rs,←⊂,'&'
         :EndIf
         :If ~##.G.Comments
             ss,←⊂'⍝\N*$'
             rs,←⊂,'&'
         :EndIf
         ss,←{⊂⍣(~1=≡ss)⊣{1↓↑,/(⎕UCS 13),¨⍵}⍣(↑1<≡⍵)⊣⍵}##.G.SearchFor
         rs,←⊂⍣(~1=≡ss)⊣'<span class="diff">&</span>'
         body2←ss ⎕R rs⍠options⊣body
         body2←(∨/¨'<span class="diff">'∘⍷¨body2)/body2
         body2←{'<div class="comp">',⍵,'</div>'}¨body2
       ⍝  body2,¨←⊂'</br>'
         r,←⊂↑,/body2
     :EndIf
 :EndIf
