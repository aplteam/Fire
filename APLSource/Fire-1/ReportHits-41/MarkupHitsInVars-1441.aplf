 r←rn MarkupHitsInVars(ref name qnc ind length);rs;ss;options;data;fullName;props
⍝ rn=Running number
 r←''
 :If ~0∊⍴data←ref.{6::'' ⋄ ⍎⍵}name
     :If ~##.Replace.NonReplaceVars data
         :If (1=≡data)∧1=⍴⍴data
             r,←MarkupHitsInSimpleVars data name rn ref
         :Else
             options←('IC'(~##.G.Case))('Greedy' 0)('EOL' 'CR'),##.IsVersion16OrBetter/⊂('UCP' 1)
             ss←rs←'' ⍝ ss:Search String; rs=Replace string
             :If ~##.G.Text
                 ss,←⊂'''\N*?'''
                 rs,←⊂,'&'
             :EndIf
             ss,←⊂⍣(~1=≡ss)⊣##.G.SearchFor
             rs,←⊂⍣(~1=≡ss)⊣'<span class="diff">&</span>'
             data←ref.⍎name
             r←ss ⎕R rs⍠options⊣data
             r←(∨/¨'<span class="diff"'∘⍷¨r)/r
             fullName←(⍕ref),'.',name
             r←(⊂'<h2 id="h',(⍕rn),'">',(⍕rn),'. ',fullName,'</h2>'),r,⊂'</div>'
             r←(⊂'<div class="obj">'),r,⊂'</div>'
         :EndIf
     :Else
         fullName←(⍕ref),'.',name
         props←{'Shape: ',(⍕⍴⍵),'; Rank: ',(⍕⍴⍴⍵),'; Depth: ',(⍕≡⍵)}⍎fullName
         props,←'; Data type: '
         :Select {(∪1↑¨0⍴¨∊⍵)≡¨,¨' ' 0}⍎fullName
         :Case 1 0
             props,←'Char'
         :Case 0 1
             props,←'Numeric'
         :Else
             props,←'Mixed'
         :EndSelect
         r←⊂('h2 id="h',(⍕rn),'"')Tag(⍕rn),'. ',fullName
         r,←⊂'p'Tag props
         r←(⊂'<div class="obj">'),r,⊂'</div>'
     :EndIf
 :EndIf
