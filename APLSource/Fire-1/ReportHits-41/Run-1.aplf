 {r}←{noGui}Run testFlag;htmlFilename;write;data;msg;buffer;n
⍝ Creates a report with all hits.
⍝ Usually `r` is the `n` namespace of the "Hit report" GUI. However, in case the (optional)
⍝ left argument is 1 no GUI is shown at all. Instead the HTML is returnd. This is for test cases only.
 r←⍬
 noGui←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'noGui'
 htmlFilename←##.GetTempFileName'htm'
 ##.A.WriteUtf8File htmlFilename(TopOfDoc GetCss ⍬)
 write←'append'∘{⍺ ##.A.WriteUtf8File htmlFilename ⍵}
 :If 0≠≢data←##.G.Hits
     {}{'Compile report...'##.EnableProgressbar 1+≢⍵}⍣(0=noGui)⊣data
     :If ##.G.Negate
         :If 0=testFlag
         :AndIf 0=noGui
             msg←'Nothing to report!' 'The last search was performed with "Negate" ticked.'
             msg,←⊂'Perform "Search" again with "Search hit list" ticked and "Negate" unticked.'
             {}##.Question.ShowMsg msg
         :EndIf
     :Else
         :If ##.G.SearchIsRegEx
             buffer←(MarkUpRegExHits MarkUpAllHits)↓data
         :Else
             buffer←(MarkUpHits MarkUpAllHits)↓data
         :EndIf
         'append'##.A.WriteUtf8File htmlFilename(buffer)
         'append'##.A.WriteUtf8File htmlFilename BottomOfDoc
         :If noGui
             r←##.A.ReadUtf8File htmlFilename
             ##.F.DeleteFile htmlFilename
         :Else
             n←testFlag Display htmlFilename data
             n.∆Filename←htmlFilename
             r←n
         :EndIf

     :EndIf
     {}{##.DisableProgressbar ⍵}⍣(0=noGui)⊣⍬
 :EndIf
