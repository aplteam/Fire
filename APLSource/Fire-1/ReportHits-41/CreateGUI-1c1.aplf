 n←CreateGUI dummy;∆;rf;b;cursorWas;buff
 cursorWas←##.GetCursorObj
 n←⎕NS''
 n.firstFlag←1
 ∆←⊂'Form'
 ∆,←⊂'Caption'(##.AddPidToCaption'Fire - Hit Report')
 ∆,←⊂'Coord' 'Pixel'
 ∆,←⊂'Icon'##.GUI.n.TheIcon

 n.TheForm←⍎'ff'##.GUI.n.Form.⎕WC ∆
 n.TheForm.onKeyPress←'OnKeyPress'
 n.TheForm.onContextMenu←'OnContextMenu'
 n.TheForm.onClose←'OnClose'
 n.TheForm.Posn←n.TheForm.Size ##.GUI.CenterIn ##.GUI.n.Form
 n.TheForm.n←n

 'MB'n.TheForm.⎕WC'Menubar'
 :If ##.G.APL_Name
     'CMDs'n.TheForm.MB.⎕WC'Menu'('Caption' 'Special')
     rf←⍎'NameList'n.TheForm.MB.CMDs.⎕WC'MenuItem'('Caption' 'Show list of names')('Active'##.G.APL_Name)
     rf.onSelect←'OnNameList'
 :EndIf

 ∆←⊂'Button'
 ∆,←⊂'Caption' 'Browse Code'
 ∆,←⊂'Posn'(¯3 ¯3)
 ∆,←⊂'Size'(1 1)
 ∆,←⊂'Accelerator'(69 2)
 rf←⍎'BrowseCode'n.TheForm.⎕WC ∆
 rf.onSelect←'OnBrowseCode'n

 ∆←⊂'Button'
 ∆,←⊂'Caption' 'Remove from hit list'
 ∆,←⊂'Posn'(¯3 ¯3)
 ∆,←⊂'Size'(1 1)
 ∆,←⊂'Accelerator'(46 0)
 rf←⍎'Del'n.TheForm.⎕WC ∆
 rf.onSelect←'OnDel'

 n.APL_Font←⍎'fnt'n.TheForm.⎕WC'Font' 'APL385 Unicode' 18
 'e1'n.TheForm.⎕WC'Edit'('Posn'(¯5 ¯5))('Size'(2 2))('Font'n.APL_Font) ⍝ For GetTextsSize only

 ∆←⊂'SubForm'
 ∆,←⊂'Posn'(0 0)
 ∆,←⊂'Size'(n.TheForm.Size)
 ∆,←⊂'Attach'('Top' 'Left' 'Bottom' 'Right')
 n.MainSubForm←⍎'sf1'n.TheForm.⎕WC ∆

 ∆←⊂'ListView'
 ∆,←⊂'ColTitles'('No' 'Name' 'Type' 'Actions')
 ∆,←⊂'Items'(⍕¨1+⍳⍴##.GUI.n.HitList.Items)
 buff←⍪##.GUI.n.HitList.ReportInfo[;0],¨'.',¨##.GUI.n.HitList.Items
 buff,←{b←~(w←⍵)∊⎕D ⋄ (b/w)←'.' ⋄ w}¨⍕¨##.GUI.n.HitList.ReportInfo[;2]
 buff,←(↑⍴##.GUI.n.HitList.ReportInfo)⍴⊂(⍴'deleted')⍴' '
 ∆,←⊂'ReportInfo'buff
 ∆,←⊂'View' 'Report'
 ∆,←⊂'Posn'(0 0)
 ∆,←⊂'Size'(n.MainSubForm.Size[0],100+1⊃n.TheForm.e1.GetTextSize⊂' '⍴⍨↑⌈/↑¨+/⍴¨buff)
 ∆,←⊂'Font'n.APL_Font
 ∆,←⊂'FullRowSelect' 1
 n.List←⍎'list'n.MainSubForm.⎕WC ∆
 {n.List.SetColSize ⍵ ¯3}¨⍳4
 n.List.onItemDblClick←'OnDblClick'
 n.List.onKeyPress←'OnKeyPress'
 n.List.onItemUp←'OnItemUpInListView'
 n.List.on9994←'On9994'
 ##.GUI.SetFocusTo n.List 0

 ∆←⊂'SubForm'
 ∆,←⊂'Posn'(0,n.List.Size[1]+5)
 ∆,←⊂'Size'((n.TheForm.Size[0]-80)(100+n.TheForm.Size[1]-n.List.Size[1]+5))
 n.SF2←⍎'sf2'n.MainSubForm.⎕WC ∆

 rf←⍎'WEBBROWSER'n.TheForm.⎕WC'OCXClass' 'Microsoft Web Browser'

 ∆←⊂⍕rf ⍝ 'WEBBROWSER'
 ∆,←⊂'Coord' 'Prop'
 ∆,←⊂'Size' 100 100
 ∆,←⊂'Posn' 0 0
 ∆,←⊂'QueueEvents' 1
 n.Browser←⍎'wb'n.SF2.⎕WC ∆

 ∆←⊂'Splitter'
 ∆,←⊂'Posn'(0,n.List.Size[1]+1)
 ∆,←⊂'SplitObj1'n.List
 ∆,←⊂'SplitObj2'n.SF2
 ∆,←⊂'Align' 'Left'
 n.Splitter←⍎'splitter'n.MainSubForm.⎕WC ∆

 ##.SetCursorObj cursorWas
⍝Done
