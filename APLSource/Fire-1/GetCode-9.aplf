 (body nc)←ref GetCode name;itemNo
⍝ Result:
⍝ [1] The code for 3,4, classes and scripted namespaces,
⍝     ,⎕fmt for variables and '' for everything else
⍝ [2] Name class
 nc←ref.⎕NC name
 :If 0=nc
     ⎕SIGNAL 918            ⍝ Then it is a shadowed Fire variable - we cannot solve this!
 :Else
     :If 2=nc
         body←ref.⍎name
         nc←2.1
     :ElseIf 3=nc
         body←ref.⎕NR name
     :ElseIf 4=nc
         body←ref.⎕NR name
     :Else
         body←{0::⍬ ⋄ ⎕SRC ⍵}ref
         :If 0∊⍴body
             :Trap 0
                 :If 0∊⍴name  ⍝ If ⍵ is empty then ⍵ is a class instance
                     (body nc)←(⎕SRC 0 0⊃⎕CLASS ref)9.2
                 :Else
                     body←{0::⍬ ⋄ ⎕SRC 0 0⊃⎕CLASS ⍵}ref
                     :If ⍬≢body
                         nc←9.2
                     :Else
                         body←ref.⎕SRC ref.⍎name
                         nc←ref.⎕NC⊂,name
                     :EndIf
                 :EndIf
             :Else
                 (body nc)←⊂''  ⍝ No idea
             :EndTrap
         :Else
             itemNo←↑∊body∘{('^ *',⍵)⎕S 2⍠('IC' 1)('Mode' 'L')⊣⍺}¨':namespace' ':class' ':interface'
             nc←(':namespace' ':class' ':interface'⍳⊂A.Lowercase{⍵↑⍨⍵⍳' '}A.dlb itemNo⊃body)⊃9.1 9.4 9.5
         :EndIf
     :EndIf
 :EndIf
