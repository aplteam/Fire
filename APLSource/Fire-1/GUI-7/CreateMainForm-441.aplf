 n←CreateMainForm(debugFlag n);∆;height

 height←600 330    ⍝ [1]: Initial value; [2]: Used when F8 is pressed

 ∆←⊂'Form'
 ∆,←⊂'Caption'(##.AddPidToCaption'Fire - ',##.WSID)
 ∆,←⊂'Coord' 'Pixel'
 ∆,←⊂'Size'##.G.Size
 ∆,←⊂'Posn'##.G.Posn
 ∆,←⊂'IconObj'n.TheIcon
 n.Form←⍎'TheForm'⎕WC ∆
 n.Form.CursorObj←0
 n.Form.height←height
 n.Form.onClose←'OnClose'
 n.Form.onKeyPress←'OnKeyPressStandard'
 n.Form.onHelp←'OnHelp'
 n.Form.Event,←⊂9955 1
 n.Form.LastKey←⍬
 n.Form.CursorObj←0
 n.Form.n←n

 CreateFonts
 n.Form.FontObj←n.Std_Font

 ∆←⊂'TipField'
 ∆,←⊂'FontObj'n.Tip_Font
 n.TipObj←⍎'TheTip'n.Form.⎕WC ∆

 CreateStatusbar
 CreateMenubar debugFlag
 ⍝Done
