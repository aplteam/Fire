 (rc newCode)←nc ChangeItByRegEx oldCode;i;this;lwh;st;type;isScripted;msg;options;ss;rs
⍝ Perform all necessary replace operations.
 oldCode←,oldCode
 newCode←''
 rc←0
 :Trap 5 11
     ss←rs←'' ⍝ Search string (ss) and Replace string (rs)
     :If 2>≡oldCode
         options←('DotAll' 0)('IC'(~##.G.Case))('Greedy'##.G.Greedy),##.IsVersion16OrBetter/⊂('UCP' 1)
     :Else
         options←('Mode' 'M')('DotAll'##.G.DotAll)('IC'(~##.G.Case))('Greedy'##.G.Greedy)('EOL' 'CR'),##.IsVersion16OrBetter/⊂('UCP' 1)
     :EndIf
     :If ~##.G.Text
         ss,←⊂'''\N*?'''
         rs,←⊂,'&'
     :EndIf
     :If 2≠⌊nc
     :AndIf ~##.G.Comments
         ss,←⊂'⍝.*$'
         rs,←⊂,'&'
     :EndIf
     ss,←⊂⍣(1<≡ss)⊣##.G.SearchFor
     rs,←⊂⍣(1<≡ss)⊣{1=⍴,⍵:↑⍵ ⋄ 1↓↑,/(⎕UCS 13),¨⍵}##.G.ReplaceBy
     newCode←ss ⎕R rs⍠options⊣oldCode
 :Else
     msg←⊂'There seems to be a problem with the Regular Expression(s).'
     msg,←'' 'Error message:'
     :If 0∊⍴⎕DMX.Message
         msg,←⎕DM[0]
     :Else
         msg,←⊂⎕DMX.Message
     :EndIf
     'Regular Expression problem'##.Question.ShowMsg msg
     rc←1
 :EndTrap
⍝Done
