 newCode←nc ChangeIt(oldCode ind);i;this;lwh;st;isScripted
⍝ Perform all necessary replace operations.
 oldCode←,oldCode
 :If n.DeleteLine.State
     lwh←∪+⌿({⍵+⍳⍴⍵}++\↑∘⍴¨oldCode)∘.<ind                       ⍝ Lines with hits
     :If 2=⌊nc
         :If 2=≡oldCode                                         ⍝ Has no impact whatsoever on simple text
             :If lwh≡⍳⍴oldCode                                  ⍝ All lines carry hits?
                 newCode←⊂''                                    ⍝ That's it!
             :Else
                 newCode←oldCode[(⍳⍴oldCode)~lwh]
             :EndIf
         :Else
             newCode←''                                         ⍝ That's it!
         :EndIf
     :Else
         :If lwh≡⍳⍴oldCode                                      ⍝ All lines carry hits?
         :AndIf ~(⊂{##.A.Lowercase ⍵↑⍨⍵⍳' '}0⊃oldCode)∊':class' ':namespace' ':interface'
             newCode←''                                         ⍝ Then we remove it!
         :Else
             lwh~←0                                             ⍝ But never line zero
             :If ∨/':interface' ':class'{⍺∊⊂⍵↑⍨⍵⍳' '}##.A.Lowercase 0⊃oldCode ⍝ Is it a class or an interface?
             :OrIf nc∊3.2 4.2 9.1                               ⍝ Or a dfns/dop?
                 lwh~←¯1+⍴oldCode                               ⍝ With those we MUST preserve the last line
             :EndIf
             st←':interface' ':class' ':namespace'              ⍝ Script types
             :If ∨/st{⍺∊⊂⍵↑⍨⌊/⍵⍳' ⍝'}##.A.Lowercase 0⊃oldCode   ⍝ Is it a script?
                 lwh/⍨←{~'∇'∊MaskText ⍵}¨oldCode[lwh]           ⍝ Mask header lines of tradfns
             :EndIf
             newCode←oldCode[(⍳⍴oldCode)~lwh]
         :EndIf
     :EndIf
 :Else
     :If 2=⌊nc
     :AndIf 2>≡oldCode
         newCode←Replace oldCode ##.G.SearchFor2 ##.G.ReplaceBy ind
     :Else
         newCode←1↓Simplify ##.CR,¨oldCode
         newCode←Replace newCode ##.G.SearchFor2 ##.G.ReplaceBy ind
         newCode←##.CR ##.A.Split newCode
         :If 2≠⌊nc
             newCode(MarkChanges)←oldCode
         :EndIf
     :EndIf
 :EndIf
