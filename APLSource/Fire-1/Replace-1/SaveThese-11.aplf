 (filename fno)←SaveThese dummy;thisObj;width;thisLoc;thisType;name;code;thisNC;data
⍝ Write source code of all objects in ##.GUI.n.HitList.Data to a native file
 width←80
 data←##.GetSortedHitListData ⍬
 :If ~0∊⍴data
     filename←0 ##.GetTempFileName'.dcf'
     fno←filename ⎕FCREATE 0
     :For thisLoc thisObj thisType thisNC :In ↓data
         name←(⍕thisLoc),{(0∊⍴⍵):⍵ ⋄ '.',⍵}thisObj
         :Select ⌊↑thisNC
         :Case 2
             code←thisLoc.⍎thisObj      ⍝ VALUE ERROR?! The object has disappeared since "Find" was triggered
         :CaseList 3 4
             code←thisLoc.⎕NR thisObj
         :Case 9
             code←⎕SRC thisLoc⍎thisObj  ⍝ VALUE ERROR?! The object has disappeared since "Find" was triggered
         :Else
             . ⍝ Huuh?!
         :EndSelect
         name code ⎕FAPPEND fno
     :EndFor
 :EndIf
