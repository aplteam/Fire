 {r}←CompileReport G;A;fno;firstFlag;k;searchFor;replaceBy;realSequence;wantedSequence;noOfItems
⍝ Compiles a full report. Unchanged lines are shown once but enclosed with <div class="show">
⍝ so that we can hide them all in one go via CSS.
⍝ All changed lines are shown twice, with the changes emphasized, again via CSS.
⍝ "compressType": 1=full report ⋄ 2=compressed report
 r←⍬ ⋄ A←##.A
 A.WriteUtf8File G.htmlFilename(##.ReportDiffs.TopOfDoc 1⊃A.SplitPath G.cssFilename)
 fno←G.htmlFilename ⎕NTIE 0
 WriteReport←fno∘{(0∊⍴⍵): ⋄ 'append'A.WriteUtf8File ⍺ ⍵}
 {}'Compile report...'##.EnableProgressbar noOfItems←-/⌽2↑⎕FSIZE G.compareFileTieNo
 firstFlag←1
 k←0
 searchFor←##.G.NamesOnly ##.IfLower G.SearchFor
 replaceBy←##.A.dlb ##.G.NamesOnly ##.IfLower G.ReplaceBy
 realSequence←⊃¨{(⍕⍺),'.',⍵}/¨↓(##.GetSortedHitListData ⍬)[;0 1]
 wantedSequence←⊃¨{(⍕⍺),'.',⍵}/¨↓##.G.Hits[;0 1]
 :Repeat
     {(0=5|k):##.GUI.n.PB.Thumb←⍵}k
     CompileReport_ k(1+realSequence⍳wantedSequence[k])firstFlag G
     firstFlag←0
 :Until noOfItems=k←k+1
 'append'A.WriteUtf8File fno(⊂'</div>')
 'append'A.WriteUtf8File fno BottomOfDoc
 ⎕NUNTIE fno
