 r←RemoveUnwanted list;nc;list2;buff
⍝ Remove unwanted objects according to user decisions in the "Object types" group:
 :If G.ReuseSearch          ⍝ We don't do this when "Search hit list" is ticked!
     r←list
 :Else
     :If 0<G.(Vars+Code)
         nc←(G.Vars/2.1),G.Code/3.1 4.1 3.2 4.2 9.4 9.5
         r←0⌿list2←(list[;2]∊nc)⌿list
         list2⍪←((list[;2]∊9 9.1)∨list[;3]=Types.ScriptedNamespace)⌿list
         :If G.Code
         :AndIf ~0∊⍴list2
             r⍪←(~list2[;2]∊2.1 9)⌿list2
         :EndIf
         :If G.Vars
         :AndIf ~0∊⍴list2
             r⍪←(list2[;2]=2.1)⌿list2
         :EndIf
     :Else
         r←list2←list
     :EndIf
     :If ~0∊⍴list2
     :AndIf 0=G.(Code+Vars)
         r⍪←(list2[;3]=Types.ScriptedNamespace)⌿list2
         ⍝ We need to add the namespace names as such, otherwise they won't be found at all:
         buff←(list2[;2]=9)⌿list2     ⍝ Pure namespaces
         r⍪←(~buff[;1]∊r[;0])⌿buff    ⍝ Only if not already in `r` anyway
     :Else
         r⍪←((list2[;3]=Types.ScriptedNamespace)∧(list2[;2]∊9.1 3.1 3.2 4.1 4.2))⌿list2
     :EndIf
     r←({(⍵⍳⍵)=⍳⍴⍵}↑¨{(⍕⍺),'.',⍵}/¨↓r[;0 1])⌿r   ⍝ Remove doubles
 :EndIf
⍝Done
